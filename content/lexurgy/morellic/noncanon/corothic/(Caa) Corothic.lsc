Feature +breathy, +rounded, +long, +syllabic
Feature Frontness(front, nearfront, central, nearback, back)
Feature Height(low, nearlow, lowmid, mid, highmid, nearhigh, high)
Feature Place(labial, labiodental, dental, alveolar, postalveolar, retroflex, alveopalatal, palatal, velar, uvular, pharyngeal, epiglottal, glottal)
Feature Manner(plosive, fricative, affricate, trill, flap, approximant, lateralapproximant, lateralfricative, lateralaffricate, nasal)
Feature +voiced, +labialised, +palatalised, +aspirated, +nasalised, +rhotic, +retracted, +prenasalised, +pharyngealised, +ejective, +breathyvoice, +noaudiblerelease
Feature Tone(*notone, lowtone, midtone, hightone)
Feature (syllable) +heavy
Feature (syllable) +stress

Diacritic ʱ [+breathy]
Diacritic ̤ [+breathyvoice]
Diacritic ̃ [+nasalised]
Diacritic ̥ [-voiced]
Diacritic ̬ [+voiced]
Diacritic ̹ [+rounded]
Diacritic ̜ [-rounded]
Diacritic ˈ (before) [+stress]
Diacritic ᴴ [+heavy]
Diacritic ̠ [+retracted]
Diacritic ̩ [+syllabic]
Diacritic ̯ [-syllabic]
Diacritic ́ (floating) [hightone]
Diacritic ̀ (floating) [lowtone]
Diacritic ̄ (floating) [midtone]
Diacritic ˞ [+rhotic]
Diacritic ʰ [+aspirated]
Diacritic ʷ [+labialised]
Diacritic ʲ [+palatalised]
Diacritic ː (floating) [+long]
Diacritic ⁿ (before) [+prenasalised]
Diacritic ˤ [+pharyngealised]
Diacritic ʼ (floating) [+ejective]
Diacritic ̚ [+noaudiblerelease]

Symbol i [front high -rounded +voiced +syllabic]
Symbol y [front high +rounded +voiced +syllabic]
Symbol ɪ [nearfront nearhigh -rounded +voiced +syllabic]
Symbol ʏ [nearfront nearhigh +rounded +voiced +syllabic]
Symbol e [front highmid -rounded +voiced +syllabic]
Symbol ø [front highmid +rounded +voiced +syllabic]
Symbol e̞ [front mid -rounded +voiced +syllabic]
Symbol ø̞ [front mid +rounded +voiced +syllabic]
Symbol ɛ [front lowmid -rounded +voiced +syllabic]
Symbol œ [front lowmid +rounded +voiced +syllabic]
Symbol a [front low -rounded +voiced +syllabic]
Symbol ɶ [front low +rounded +voiced +syllabic]
Symbol æ [front nearlow -rounded +voiced +syllabic]
Symbol ɨ [central high -rounded +voiced +syllabic]
Symbol ʉ [central high +rounded +voiced +syllabic]
Symbol ɘ [central highmid -rounded +voiced +syllabic]
Symbol ɵ [central highmid +rounded +voiced +syllabic]
Symbol ə [central mid -rounded +voiced +syllabic]
Symbol ɵ̞ [central mid +rounded +voiced +syllabic]
Symbol ɜ [central lowmid -rounded +voiced +syllabic]
Symbol ɞ [central lowmid +rounded +voiced +syllabic]
Symbol ä [central low -rounded +voiced +syllabic]
Symbol ɶ̈ [central low +rounded +voiced +syllabic]
Symbol ɐ [central nearlow -rounded +voiced +syllabic]
Symbol u [back high +rounded +voiced +syllabic]
Symbol ɯ [back high -rounded +voiced +syllabic]
Symbol ʊ [nearback nearhigh +rounded +voiced +syllabic]
Symbol ʊ̹ [nearback nearhigh -rounded +voiced +syllabic]
Symbol o [back highmid +rounded +voiced +syllabic]
Symbol ɤ [back highmid -rounded +voiced +syllabic]
Symbol o̞ [back mid +rounded +voiced +syllabic]
Symbol ɤ̞ [back mid -rounded +voiced +syllabic]
Symbol ɔ [back lowmid +rounded +voiced +syllabic]
Symbol ʌ [back lowmid -rounded +voiced +syllabic]
Symbol ɒ [back low +rounded +voiced +syllabic]
Symbol ɑ [back low -rounded +voiced +syllabic]

Symbol p [-voiced labial plosive -syllabic]
Symbol ɸ [-voiced labial fricative -syllabic]
Symbol p͡ɸ [-voiced labial affricate -syllabic]
Symbol b [+voiced labial plosive -syllabic]
Symbol ᵐb [+voiced labial plosive -syllabic +prenasalised]
Symbol β [+voiced labial fricative -syllabic]
Symbol b͡β [+voiced labial affricate -syllabic]
Symbol ʙ [+voiced labial trill -syllabic]
Symbol ʙ̆ [+voiced labial flap -syllabic]
Symbol m [+voiced labial nasal -syllabic]
Symbol β̞ [+voiced labial approximant -syllabic]

Symbol p̪ [-voiced labiodental plosive -syllabic]
Symbol f [-voiced labiodental fricative -syllabic]
Symbol p̪͡f [-voiced labiodental affricate -syllabic]
Symbol b̪ [+voiced labiodental plosive -syllabic]
Symbol v [+voiced labiodental fricative -syllabic]
Symbol b̪͡v [+voiced labiodental affricate -syllabic]
Symbol ʙ̠ [+voiced labiodental trill -syllabic]
Symbol ⱱ [+voiced labiodental flap -syllabic]
Symbol ɱ [+voiced labiodental nasal -syllabic]
Symbol ʋ [+voiced labiodental approximant -syllabic]

Symbol t [-voiced alveolar plosive -syllabic]
Symbol s [-voiced alveolar fricative -syllabic]
Symbol t͡s [-voiced alveolar affricate -syllabic]
Symbol d [+voiced alveolar plosive -syllabic]
Symbol z [+voiced alveolar fricative -syllabic]
Symbol d͡z [+voiced alveolar affricate -syllabic]
Symbol r [+voiced alveolar trill -syllabic]
Symbol ɾ [+voiced alveolar flap -syllabic]
Symbol n [+voiced alveolar nasal -syllabic]
Symbol ɹ [+voiced alveolar approximant -syllabic]
Symbol l [+voiced alveolar lateralapproximant -syllabic]
Symbol ɬ [-voiced alveolar lateralfricative -syllabic]
Symbol ɮ [+voiced alveolar lateralfricative -syllabic]
Symbol t͡ɬ [-voiced alveolar lateralaffricate -syllabic]
Symbol d͡ɮ [+voiced alveolar lateralaffricate -syllabic]

Symbol t̠ [-voiced postalveolar plosive -syllabic]
Symbol ʃ [-voiced postalveolar fricative -syllabic]
Symbol t͡ʃ [-voiced postalveolar affricate -syllabic]
Symbol d̠ [+voiced postalveolar plosive -syllabic]
Symbol ʒ [+voiced postalveolar fricative -syllabic]
Symbol d͡ʒ [+voiced postalveolar affricate -syllabic]
Symbol r̠ [+voiced postalveolar trill -syllabic]
Symbol ɾ̠ [+voiced postalveolar flap -syllabic]
Symbol n̠ [+voiced postalveolar nasal -syllabic]
Symbol ɹ̠ [+voiced postalveolar approximant -syllabic]
Symbol l̠ [+voiced postalveolar lateralapproximant -syllabic]

Symbol t̪ [-voiced dental plosive -syllabic]
Symbol θ [-voiced dental fricative -syllabic]
Symbol t͡θ [-voiced dental affricate -syllabic]
Symbol d̪ [+voiced dental plosive -syllabic]
Symbol ð [+voiced dental fricative -syllabic]
Symbol d͡ð [+voiced dental affricate -syllabic]
Symbol r̪ [+voiced dental trill -syllabic]
Symbol ɾ̪ [+voiced dental flap -syllabic]
Symbol n̪ [+voiced dental nasal -syllabic]
Symbol ð̞ [+voiced dental approximant -syllabic]
Symbol θ̞ [-voiced dental approximant -syllabic]
Symbol l̪ [+voiced dental lateralapproximant -syllabic]

Symbol ʈ [-voiced retroflex plosive -syllabic]
Symbol ʂ [-voiced retroflex fricative -syllabic]
Symbol t͡ʂ [-voiced retroflex affricate -syllabic]
Symbol ɖ [+voiced retroflex plosive -syllabic]
Symbol ʐ [+voiced retroflex fricative -syllabic]
Symbol d͡ʐ [+voiced retroflex affricate -syllabic]
Symbol ɽː [+voiced retroflex trill -syllabic]
Symbol ɽ [+voiced retroflex flap -syllabic]
Symbol ɳ [+voiced retroflex nasal -syllabic]
Symbol ɻ [+voiced retroflex approximant -syllabic]
Symbol ɭ [+voiced retroflex lateralapproximant -syllabic]

Symbol c̟ [-voiced alveopalatal plosive -syllabic]
Symbol ɕ [-voiced alveopalatal fricative -syllabic]
Symbol t͡ɕ [-voiced alveopalatal affricate -syllabic]
Symbol ɟ̟ [+voiced alveopalatal plosive -syllabic]
Symbol ʑ [+voiced alveopalatal fricative -syllabic]
Symbol d͡ʑ [+voiced alveopalatal affricate -syllabic]
Symbol ɟ̟̆ː [+voiced alveopalatal trill -syllabic]
Symbol ɟ̟̆ [+voiced alveopalatal flap -syllabic]
Symbol ɲ̟ [+voiced alveopalatal nasal -syllabic]
Symbol j̟ [+voiced alveopalatal approximant -syllabic]
Symbol ʎ̟ [+voiced alveopalatal lateralapproximant -syllabic]

Symbol c [-voiced palatal plosive -syllabic]
Symbol ç [-voiced palatal fricative -syllabic]
Symbol c͡ç [-voiced palatal affricate -syllabic]
Symbol ɟ [+voiced palatal plosive -syllabic]
Symbol ᶮɟ [+voiced +prenasalised palatal plosive -syllabic]
Symbol ʝ [+voiced palatal fricative -syllabic]
Symbol ɟ͡ʝ [+voiced palatal affricate -syllabic]
Symbol ɟ̆ː [+voiced palatal trill -syllabic]
Symbol ɟ̆ [+voiced palatal flap -syllabic]
Symbol ɲ [+voiced palatal nasal -syllabic]
Symbol j [+voiced palatal approximant -syllabic]
Symbol ɥ [+voiced palatal approximant +labialised -syllabic]
Symbol ʎ [+voiced palatal lateralapproximant -syllabic]

Symbol k [-voiced velar plosive -syllabic]
Symbol x [-voiced velar fricative -syllabic]
Symbol k͡x [-voiced velar affricate -syllabic]
Symbol g [+voiced velar plosive -syllabic]
Symbol ᵑg [+voiced velar plosive -syllabic +prenasalised]
Symbol ɣ [+voiced velar fricative -syllabic]
Symbol g͡ɣ [+voiced velar affricate -syllabic]
Symbol ŋ [+voiced velar nasal -syllabic]
Symbol ŋ̊ [-voiced velar nasal -syllabic]
Symbol ɰ [+voiced velar approximant -syllabic]
Symbol w [+voiced velar approximant +labialised -syllabic]
Symbol ʍ [-voiced velar approximant +labialised -syllabic]
Symbol ʟ [+voiced velar lateralapproximant -syllabic]

Symbol q [-voiced uvular plosive -syllabic]
Symbol χ [-voiced uvular fricative -syllabic]
Symbol q͡χ [-voiced uvular affricate -syllabic]
Symbol ɢ [+voiced uvular plosive -syllabic]
Symbol ʁ [+voiced uvular fricative -syllabic]
Symbol ɢ͡ʁ [+voiced uvular affricate -syllabic]
Symbol ʀ [+voiced uvular trill -syllabic]
Symbol ɢ̆ [+voiced uvular flap -syllabic]
Symbol ɴ [+voiced uvular nasal -syllabic]
Symbol ʁ̞ [+voiced uvular approximant -syllabic]
Symbol ʟ̠ [+voiced uvular lateralapproximant -syllabic]

Symbol ħ [-voiced pharyngeal fricative -syllabic]
Symbol ʕ [+voiced pharyngeal fricative -syllabic]

Symbol ʡ [-voiced epiglottal plosive -syllabic]
Symbol ʜ [-voiced epiglottal fricative -syllabic]
Symbol ʡ͡ʜ [-voiced epiglottal affricate -syllabic]
Symbol ʢ [+voiced epiglottal fricative -syllabic]

Symbol ʔ [glottal plosive -syllabic]
Symbol h [-voiced glottal fricative -syllabic]
Symbol ʔ͡h [-voiced glottal affricate -syllabic]
Symbol ɦ [+voiced glottal fricative -syllabic]

Element consonant {[plosive], [fricative], [affricate], [trill], [flap], [approximant], [lateralapproximant], [lateralfricative], [lateralaffricate], [nasal]}
Element vowel {[front], [nearfront], [central], [nearback], [back]}
Element obstruent {[affricate], [plosive], [fricative], [lateralfricative], [lateralaffricate]}
Element sonorant {[!affricate !plosive !fricative !lateralfricative !lateralaffricate]&@consonant}
Element continuant {@sonorant, [fricative], [lateralfricative]}
Element laryngeal {[glottal], [epiglottal], [pharyngeal]}
Element labial {[labiodental], [labial]}
Element lax @vowel&{[nearfront], [nearhigh], [nearlow central]}
Element nonlax @vowel&!{[nearfront], [nearhigh], [nearlow central]}

contraction:
 \-siam => \-si

deromanisation:
 {ng, y, kh} => {ŋ, ɣ, x}
 then: (
 {[plosive +voiced], [nasal], [lateralapproximant]} h => [+breathy] *
 {{â, â}, {ê, ê}, {î, î}, {ô, ô}, {û, û}, ï̂, ü̂} => {aː, eː, iː, oː, uː, ɯː, yː}
 {ï, ü} => {ɯ, y}
 )
 then: * => h / @vowel _ @vowel
 then: \' => *

glomination:
 @vowel \- / _ @vowel => *
 \- => *

nasal-assimilation defer:
 [nasal] => [$Place] / _ @consonant&[$Place]
 then: (
 [labiodental nasal] => [labial]
 [dental nasal] => [alveolar]
 [postalveolar nasal] => [palatal]
 )

assimilation:
 @obstruent => [-voiced] / _ @continuant&[-voiced]
 then: []$1 $1 => * [+long]

syllables:
 s? @consonant? @continuant? {@vowel&[+syllabic] {@vowel&[-syllabic], {@vowel&[-syllabic]}? {[nasal], [lateralapproximant], [trill], [flap]}? @consonant}, @vowel&[+syllabic +long]} => [+heavy]
 s? @consonant? @continuant? @vowel&[+syllabic] => [-heavy]

syllables:
 explicit

romanizer-early-proto-central:
 unchanged

dentalisation:
 [alveolar plosive] => [dental]

metathesis:
 {@consonant&![approximant]}$1 @vowel$2 => $2 $1 / @consonant _ {$, .}
 then: {@consonant&![approximant]}$1 @vowel$2 => $2 $1 / @consonant _ $

frication:
 [plosive] => [fricative -breathy] // {$ _, @consonant _, _ @consonant}
 then: [labial fricative] => [labiodental]

palatalisation defer:
 [velar fricative +voiced] => [palatal approximant]
 then: [alveolar fricative] j => [postalveolar] *
 then: {[velar], [alveolar nasal]} j => [palatal] *
 then: [palatal lateralapproximant] => [approximant]
 then: [velar fricative -voiced] => [palatal]

palatalisation-1:
 :palatalisation

nasal-devoicing defer:
 :nasal-assimilation
 then: (
 [nasal -breathy] @obstruent&[$voiced $breathy] => [$voiced $breathy] *
 [nasal -breathy] => [-voiced] // _ [+voiced]
 [nasal +breathy] @continuant => * [+long]
 [nasal +breathy] @obstruent => [-breathy] []
 )

nasal-devoicing:
 :nasal-devoicing

romanizer-old-corothic:
 unchanged

retroflexation:
 {l, lʱ} => ɻ
 then: ɻ {[alveolar], [dental]} => * [retroflex]
 then: {[alveolar], [dental]} => [retroflex] / _ ɻ
 then: ɻ => ʈ / @sonorant _ @vowel

voicing-shift:
 [-voiced plosive] => [+aspirated]
 [+voiced plosive -breathy] => [-voiced]
 [+voiced plosive +breathy] => [-breathy]
 then: {mʱ, nʱ, ŋʱ} => {w, l, j}
 then: [+breathy] => [-breathy]
 then: (
 [plosive $Place +aspirated] [plosive $Place -aspirated] => [+long] *
 [plosive $Place] [plosive $Place +aspirated] => * [+long]
 )
 then: []$1 $1 => [+long] *

syllables:
 s? @consonant? @continuant? @vowel @continuant? @consonant => [+heavy]
 s? @consonant? @continuant? @vowel @continuant? => [+heavy] / _ @consonant&[+long]
 s? @consonant? @continuant? @vowel => [-heavy]

stress defer:
 [] => [-stress]
 then: (
 <syl>&[+heavy] => [+stress] // _ {{[]*}? [+heavy], $}
 else: <syl> => [+stress] / _ <syl>*2 $
 else: <syl> => [+stress] / $ _
 )
 then propagate: <syl> => [+stress] / <syl>&[+stress] <syl> _ // _ $

stress-1:
 :stress

romanizer-middle-corothic:
 unchanged

vowel-shift:
 {[front -rounded], [back +rounded]}&[high] => [highmid]
 {[front +rounded], [back -rounded]}&[high] => [central]
 [low front +long] => [back +rounded]
 [low front] => [highmid]
 [highmid back] => [low]
 [highmid front] => [high]

debuccalisation:
 f => h
 [-voiced nasal] => h // _ @vowel

fricative-devoicing:
 [+voiced fricative] => [-voiced]
 then: :nasal-devoicing

vowel-loss:
 @vowel&[-long] => * / @vowel @consonant&[-long] _ $
 @vowel&[+long] => [-long]

syllables:
 s? @consonant? @continuant? {@vowel&[+long], @vowel @continuant? @consonant} => [+heavy]
 s? @consonant? @continuant? @vowel @continuant? => [+heavy] / _ @consonant&[+long]
 s? @consonant? @continuant? @vowel => [-heavy]

diphthongisation:
 @vowel h => [+long] * / _ $
 h => * // {$, @vowel} _ @vowel
 then: []$1 $1 => [+long] *
 then: {ij, uw} => {iː, uː} // _ @vowel
 then: (
 {ɒwː, ewː, owː, ɒjː, ojː, ejː} => {ow, ow, ow, ej, ej, ej}
 {wːɒ, wːe, wːo, jːɒ, jːo, jːe} => {wo, wo, wo, je, je, je}
 )
 then: (
 {ɒw, ew, ɒj, oj} => {ow, ow, ej, ej}
 {wɒ, we, jɒ, jo} => {wo, wo, je, je}
 )
 then: {i, u} => {j, w} / @vowel _ @vowel // [high] _
 then: @vowel&[high] => [-syllabic] / @vowel _ // @vowel*2 _
 then: h => * // _ @vowel

syllables:
 s? @consonant? @continuant? {j, w}? {@vowel&[+long], @vowel @continuant? @consonant, @vowel @vowel&[-syllabic] @consonant?} => [+heavy]
 s? @consonant? @continuant? {j, w}? @vowel @continuant? @consonant? => [+heavy] / _ @consonant&[+long]
 s? @consonant? @continuant? {j, w}? @vowel => [-heavy]

final-vowel-heightening:
 [highmid] => [high] / _ $

assimilation-2:
 [nasal] => [$Place] / _ @consonant&[$Place !glottal]

length-loss:
 [+long] => [-long] / {{$, [+long]} _, _ $}

stress-2:
 :stress

romanizer-corothic:
 unchanged

stress-markers:
 @vowel&[+syllabic +stress] => [hightone]

syllables:
 clear

romanizer:
 @vowel&[-syllabic] => [+syllabic]
 then: {ɒ, ɨ́, ʉ́, ɨ, ʉ} => {a, i̋, ű, ï, ü}
 then: (
 [retroflex nasal] [retroflex] => [] [alveolar]
 [nasal $Place] => [labial] / _ @consonant&[$Place]&{[labial], [labiodental]}
 [nasal $Place] => [alveolar] / _ @consonant&[$Place]
 )
 then: [dental plosive] => [alveolar]
 then: (
 [+aspirated plosive] => [-aspirated]
 [-aspirated plosive] => [+voiced]
 )
 then: (
 ɻ => r
 [retroflex] => [alveolar] / _ ɻ
 * [retroflex] => r [alveolar]
 )
 then: [nasal -voiced] * => [+voiced] h
 then: [+long]$1 => $1 $1
 then: [+long] => [-long]
 then: @vowel => [notone] / @vowel _
 then: {ʃʃ, ʃ, ʒʒ, ʒ, ɟ, xx, x, çç, ç, j, nɟɟ, nɟ, ɲɲ, ɲ, ngg, ng, ŋŋ, ŋ, θθ, θ} => {ssh, sh, zzh, zh, j, kkh, kh, cch, ch, y, njj, nj, nny, ny, n'gg, n'g, nng, ng, tth, th}