Feature +breathy, +rounded, +long, +syllabic
Feature Frontness(front, nearfront, central, nearback, back)
Feature Height(low, nearlow, lowmid, mid, highmid, nearhigh, high)
Feature Place(labial, labiodental, dental, alveolar, postalveolar, retroflex, alveopalatal, palatal, velar, uvular, pharyngeal, epiglottal, glottal)
Feature Manner(plosive, fricative, affricate, implosive, trill, flap, approximant, lateralapproximant, lateralfricative, lateralaffricate, nasal)
Feature +voiced, +labialised, +palatalised, +aspirated, +nasalised, +rhotic, +retracted, +prenasalised, +pharyngealised, +ejective, +breathyvoice, +noaudiblerelease
Feature Tone(*notone, lowtone, midtone, hightone)
Feature (syllable) +heavy
Feature (syllable) +stress

Diacritic ʱ [+breathy]
Diacritic ̤ [+breathyvoice]
Diacritic ̃ [+nasalised]
Diacritic ̥ [-voiced]
Diacritic ̬ [+voiced]
Diacritic ̹ [+rounded]
Diacritic ̜ [-rounded]
Diacritic ˈ (before) [+stress]
Diacritic ᴴ [+heavy]
Diacritic ̠ [+retracted]
Diacritic ̩ [+syllabic]
Diacritic ̯ [-syllabic]
Diacritic ́ (floating) [hightone]
Diacritic ̀ (floating) [lowtone]
Diacritic ̄ (floating) [midtone]
Diacritic ˞ [+rhotic]
Diacritic ʰ [+aspirated]
Diacritic ʷ [+labialised]
Diacritic ʲ [+palatalised]
Diacritic ː [+long]
Diacritic ⁿ (before) [+prenasalised]
Diacritic ˤ [+pharyngealised]
Diacritic ʼ [+ejective]
Diacritic ̚ [+noaudiblerelease]

Symbol i [front high -rounded +voiced +syllabic]
Symbol y [front high +rounded +voiced +syllabic]
Symbol ɪ [nearfront nearhigh -rounded +voiced +syllabic]
Symbol ʏ [nearfront nearhigh +rounded +voiced +syllabic]
Symbol e [front highmid -rounded +voiced +syllabic]
Symbol ø [front highmid +rounded +voiced +syllabic]
Symbol e̞ [front mid -rounded +voiced +syllabic]
Symbol ø̞ [front mid +rounded +voiced +syllabic]
Symbol ɛ [front lowmid -rounded +voiced +syllabic]
Symbol œ [front lowmid +rounded +voiced +syllabic]
Symbol a [front low -rounded +voiced +syllabic]
Symbol ɶ [front low +rounded +voiced +syllabic]
Symbol æ [front nearlow -rounded +voiced +syllabic]
Symbol ɨ [central high -rounded +voiced +syllabic]
Symbol ʉ [central high +rounded +voiced +syllabic]
Symbol ɘ [central highmid -rounded +voiced +syllabic]
Symbol ɵ [central highmid +rounded +voiced +syllabic]
Symbol ə [central mid -rounded +voiced +syllabic]
Symbol ɵ̞ [central mid +rounded +voiced +syllabic]
Symbol ɜ [central lowmid -rounded +voiced +syllabic]
Symbol ɞ [central lowmid +rounded +voiced +syllabic]
Symbol ä [central low -rounded +voiced +syllabic]
Symbol ɶ̈ [central low +rounded +voiced +syllabic]
Symbol ɐ [central nearlow -rounded +voiced +syllabic]
Symbol u [back high +rounded +voiced +syllabic]
Symbol ɯ [back high -rounded +voiced +syllabic]
Symbol ʊ [nearback nearhigh +rounded +voiced +syllabic]
Symbol ʊ̹ [nearback nearhigh -rounded +voiced +syllabic]
Symbol o [back highmid +rounded +voiced +syllabic]
Symbol ɤ [back highmid -rounded +voiced +syllabic]
Symbol o̞ [back mid +rounded +voiced +syllabic]
Symbol ɤ̞ [back mid -rounded +voiced +syllabic]
Symbol ɔ [back lowmid +rounded +voiced +syllabic]
Symbol ʌ [back lowmid -rounded +voiced +syllabic]
Symbol ɒ [back low +rounded +voiced +syllabic]
Symbol ɑ [back low -rounded +voiced +syllabic]

Symbol p [-voiced labial plosive -syllabic]
Symbol ɸ [-voiced labial fricative -syllabic]
Symbol p͡ɸ [-voiced labial affricate -syllabic]
Symbol b [+voiced labial plosive -syllabic]
Symbol ɓ [+voiced labial implosive -syllabic]
Symbol ᵐb [+voiced labial plosive -syllabic +prenasalised]
Symbol β [+voiced labial fricative -syllabic]
Symbol b͡β [+voiced labial affricate -syllabic]
Symbol ʙ [+voiced labial trill -syllabic]
Symbol ʙ̆ [+voiced labial flap -syllabic]
Symbol m [+voiced labial nasal -syllabic]
Symbol β̞ [+voiced labial approximant -syllabic]

Symbol p̪ [-voiced labiodental plosive -syllabic]
Symbol f [-voiced labiodental fricative -syllabic]
Symbol p̪͡f [-voiced labiodental affricate -syllabic]
Symbol b̪ [+voiced labiodental plosive -syllabic]
Symbol ɓ̪ [+voiced labiodental implosive -syllabic]
Symbol v [+voiced labiodental fricative -syllabic]
Symbol b̪͡v [+voiced labiodental affricate -syllabic]
Symbol ʙ̠ [+voiced labiodental trill -syllabic]
Symbol ⱱ [+voiced labiodental flap -syllabic]
Symbol ɱ [+voiced labiodental nasal -syllabic]
Symbol ʋ [+voiced labiodental approximant -syllabic]

Symbol t [-voiced alveolar plosive -syllabic]
Symbol s [-voiced alveolar fricative -syllabic]
Symbol t͡s [-voiced alveolar affricate -syllabic]
Symbol d [+voiced alveolar plosive -syllabic]
Symbol ɗ [+voiced alveolar implosive -syllabic]
Symbol z [+voiced alveolar fricative -syllabic]
Symbol d͡z [+voiced alveolar affricate -syllabic]
Symbol r [+voiced alveolar trill -syllabic]
Symbol ɾ [+voiced alveolar flap -syllabic]
Symbol n [+voiced alveolar nasal -syllabic]
Symbol ɹ [+voiced alveolar approximant -syllabic]
Symbol l [+voiced alveolar lateralapproximant -syllabic]
Symbol ɬ [-voiced alveolar lateralfricative -syllabic]
Symbol ɮ [+voiced alveolar lateralfricative -syllabic]
Symbol t͡ɬ [-voiced alveolar lateralaffricate -syllabic]
Symbol d͡ɮ [+voiced alveolar lateralaffricate -syllabic]

Symbol t̠ [-voiced postalveolar plosive -syllabic]
Symbol ʃ [-voiced postalveolar fricative -syllabic]
Symbol t͡ʃ [-voiced postalveolar affricate -syllabic]
Symbol d̠ [+voiced postalveolar plosive -syllabic]
Symbol ɗ̠ [+voiced postalveolar implosive -syllabic]
Symbol ʒ [+voiced postalveolar fricative -syllabic]
Symbol d͡ʒ [+voiced postalveolar affricate -syllabic]
Symbol r̠ [+voiced postalveolar trill -syllabic]
Symbol ɾ̠ [+voiced postalveolar flap -syllabic]
Symbol n̠ [+voiced postalveolar nasal -syllabic]
Symbol ɹ̠ [+voiced postalveolar approximant -syllabic]
Symbol l̠ [+voiced postalveolar lateralapproximant -syllabic]

Symbol t̪ [-voiced dental plosive -syllabic]
Symbol θ [-voiced dental fricative -syllabic]
Symbol t͡θ [-voiced dental affricate -syllabic]
Symbol d̪ [+voiced dental plosive -syllabic]
Symbol ɗ̪ [+voiced dental implosive -syllabic]
Symbol ð [+voiced dental fricative -syllabic]
Symbol d͡ð [+voiced dental affricate -syllabic]
Symbol r̪ [+voiced dental trill -syllabic]
Symbol ɾ̪ [+voiced dental flap -syllabic]
Symbol n̪ [+voiced dental nasal -syllabic]
Symbol ð̞ [+voiced dental approximant -syllabic]
Symbol θ̞ [-voiced dental approximant -syllabic]
Symbol l̪ [+voiced dental lateralapproximant -syllabic]

Symbol ʈ [-voiced retroflex plosive -syllabic]
Symbol ʂ [-voiced retroflex fricative -syllabic]
Symbol t͡ʂ [-voiced retroflex affricate -syllabic]
Symbol ɖ [+voiced retroflex plosive -syllabic]
Symbol ᶑ [+voiced retroflex implosive -syllabic]
Symbol ʐ [+voiced retroflex fricative -syllabic]
Symbol d͡ʐ [+voiced retroflex affricate -syllabic]
Symbol ɽː [+voiced retroflex trill -syllabic]
Symbol ɽ [+voiced retroflex flap -syllabic]
Symbol ɳ [+voiced retroflex nasal -syllabic]
Symbol ɻ [+voiced retroflex approximant -syllabic]
Symbol ɭ [+voiced retroflex lateralapproximant -syllabic]

Symbol c̟ [-voiced alveopalatal plosive -syllabic]
Symbol ɕ [-voiced alveopalatal fricative -syllabic]
Symbol t͡ɕ [-voiced alveopalatal affricate -syllabic]
Symbol ɟ̟ [+voiced alveopalatal plosive -syllabic]
Symbol ʄ̟ [+voiced alveopalatal implosive -syllabic]
Symbol ʑ [+voiced alveopalatal fricative -syllabic]
Symbol d͡ʑ [+voiced alveopalatal affricate -syllabic]
Symbol ɟ̟̆ː [+voiced alveopalatal trill -syllabic]
Symbol ɟ̟̆ [+voiced alveopalatal flap -syllabic]
Symbol ɲ̟ [+voiced alveopalatal nasal -syllabic]
Symbol j̟ [+voiced alveopalatal approximant -syllabic]
Symbol ʎ̟ [+voiced alveopalatal lateralapproximant -syllabic]

Symbol c [-voiced palatal plosive -syllabic]
Symbol ç [-voiced palatal fricative -syllabic]
Symbol c͡ç [-voiced palatal affricate -syllabic]
Symbol ɟ [+voiced palatal plosive -syllabic]
Symbol ʄ [+voiced palatal implosive -syllabic]
Symbol ᶮɟ [+voiced +prenasalised palatal plosive -syllabic]
Symbol ʝ [+voiced palatal fricative -syllabic]
Symbol ɟ͡ʝ [+voiced palatal affricate -syllabic]
Symbol ɟ̆ː [+voiced palatal trill -syllabic]
Symbol ɟ̆ [+voiced palatal flap -syllabic]
Symbol ɲ [+voiced palatal nasal -syllabic]
Symbol j [+voiced palatal approximant -syllabic]
Symbol ɥ [+voiced palatal approximant +labialised -syllabic]
Symbol ʎ [+voiced palatal lateralapproximant -syllabic]

Symbol k [-voiced velar plosive -syllabic]
Symbol x [-voiced velar fricative -syllabic]
Symbol k͡x [-voiced velar affricate -syllabic]
Symbol g [+voiced velar plosive -syllabic]
Symbol ɠ [+voiced velar implosive -syllabic]
Symbol ᵑg [+voiced velar plosive -syllabic +prenasalised]
Symbol ɣ [+voiced velar fricative -syllabic]
Symbol g͡ɣ [+voiced velar affricate -syllabic]
Symbol ŋ [+voiced velar nasal -syllabic]
Symbol ŋ̊ [-voiced velar nasal -syllabic]
Symbol ɰ [+voiced velar approximant -syllabic]
Symbol w [+voiced velar approximant +labialised -syllabic]
Symbol ʍ [-voiced velar approximant +labialised -syllabic]
Symbol ʟ [+voiced velar lateralapproximant -syllabic]

Symbol q [-voiced uvular plosive -syllabic]
Symbol χ [-voiced uvular fricative -syllabic]
Symbol q͡χ [-voiced uvular affricate -syllabic]
Symbol ɢ [+voiced uvular plosive -syllabic]
Symbol ʛ [+voiced uvular implosive -syllabic]
Symbol ʁ [+voiced uvular fricative -syllabic]
Symbol ɢ͡ʁ [+voiced uvular affricate -syllabic]
Symbol ʀ [+voiced uvular trill -syllabic]
Symbol ɢ̆ [+voiced uvular flap -syllabic]
Symbol ɴ [+voiced uvular nasal -syllabic]
Symbol ʁ̞ [+voiced uvular approximant -syllabic]
Symbol ʟ̠ [+voiced uvular lateralapproximant -syllabic]

Symbol ħ [-voiced pharyngeal fricative -syllabic]
Symbol ʕ [+voiced pharyngeal fricative -syllabic]

Symbol ʡ [-voiced epiglottal plosive -syllabic]
Symbol ʜ [-voiced epiglottal fricative -syllabic]
Symbol ʡ͡ʜ [-voiced epiglottal affricate -syllabic]
Symbol ʢ [+voiced epiglottal fricative -syllabic]

Symbol ʔ [glottal plosive -syllabic]
Symbol h [-voiced glottal fricative -syllabic]
Symbol ʔ͡h [-voiced glottal affricate -syllabic]
Symbol ɦ [+voiced glottal fricative -syllabic]

Element consonant {[plosive], [implosive], [fricative], [affricate], [trill], [flap], [approximant], [lateralapproximant], [lateralfricative], [lateralaffricate], [nasal]}
Element vowel {[front], [nearfront], [central], [nearback], [back]}
Element obstruent {[affricate], [plosive], [implosive], [fricative], [lateralfricative], [lateralaffricate]}
Element sonorant {[!affricate !plosive !implosive !fricative !lateralfricative !lateralaffricate]&@consonant}
Element continuant {@sonorant, [fricative], [lateralfricative]}
Element laryngeal {[glottal], [epiglottal], [pharyngeal]}
Element labial {[labiodental], [labial]}
Element lax @vowel&{[nearfront], [nearhigh], [nearlow central]}
Element nonlax @vowel&!{[nearfront], [nearhigh], [nearlow central]}

deromanisation:
 {ng, y, kh} => {ŋ, ɣ, x}
 then: (
 {[plosive +voiced], [nasal], [lateralapproximant]} h => [+breathy] *
 {{â, â}, {ê, ê}, {î, î}, {ô, ô}, {û, û}, ï̂, ü̂} => {aː, eː, iː, oː, uː, ɯː, yː}
 {ï, ü} => {ɯ, y}
 )
 then: * => h / @vowel _ @vowel
 then: \' => *

lengthening:
 @vowel h => [+long] * // _ @vowel
 []$1 $1 => * [+long]

glomination:
 @vowel&[+long] \- / _ @vowel => [-long] *
 @vowel \- / _ @vowel => *
 \- => *

syllables:
 @consonant? {@vowel&[+syllabic] {@vowel&[-syllabic], {@vowel&[-syllabic]}? @continuant? @consonant}, @vowel&[+syllabic +long]} => [+heavy]
 s? @consonant? @continuant? {@vowel&[+syllabic] {@vowel&[-syllabic], {@vowel&[-syllabic]}? @continuant? @consonant}, @vowel&[+syllabic +long]} => [+heavy] / {$, @consonant*2} _
 @consonant? @vowel&[+syllabic] => [+heavy] / _ @consonant&[+long]
 @consonant? @vowel&[+syllabic] => [-heavy]
 s? @consonant? @continuant? @vowel&[+syllabic] => [+heavy] / {$, @consonant*2} _ @consonant&[+long]
 s? @consonant? @continuant? @vowel&[+syllabic] => [-heavy] / {$, @consonant*2} _

syllables:
 explicit

dentalisation:
 [alveolar]&!@continuant => [dental]

syllables:
 @consonant? [approximant]? {@vowel&[+syllabic] {@vowel&[-syllabic], {@vowel&[-syllabic]}? @continuant? @consonant}, @vowel&[+syllabic +long]} => [+heavy]
 s? @consonant? @continuant? {@vowel&[+syllabic] {@vowel&[-syllabic], {@vowel&[-syllabic]}? @continuant? @consonant}, @vowel&[+syllabic +long]} => [+heavy] / {$, @consonant*2} _
 @consonant? [approximant]? @vowel&[+syllabic] => [+heavy] / _ @consonant&[+long]
 @consonant? [approximant]? @vowel&[+syllabic] => [-heavy]
 s? @consonant? @continuant? @vowel&[+syllabic] => [+heavy] / {$, @consonant*2} _ @consonant&[+long]
 s? @consonant? @continuant? @vowel&[+syllabic] => [-heavy] / {$, @consonant*2} _

length-attribution:
 []$1 $1 => [+long] *

palatalisation:
 {ɣ, ɣː} => {j, jː}
 then: (
 [plosive dental] j => [postalveolar affricate] *
 [fricative alveolar] j => [postalveolar] *
 @sonorant&[alveolar !trill] j => [palatal] *
 )
 then: [back] => [front] / _ [palatal]
 then: [front +rounded highmid] => [high]

syllables:
 @consonant? [approximant]? {@vowel&[+syllabic] @continuant? @consonant, @vowel&[+syllabic +long]} => [+heavy]
 s? @consonant? @continuant? {@vowel&[+syllabic] @continuant? @consonant, @vowel&[+syllabic +long]} => [+heavy] / {$, @consonant*2} _
 @consonant? [approximant]? @vowel&[+syllabic] => [+heavy] / _ @consonant&[+long]
 @consonant? [approximant]? @vowel&[+syllabic] => [-heavy]
 s? @consonant? @continuant? @vowel&[+syllabic] => [+heavy] / {$, @consonant*2} _ @consonant&[+long]
 s? @consonant? @continuant? @vowel&[+syllabic] => [-heavy] / {$, @consonant*2} _

approximantisation:
 {v, vː} => {w, wː}
 {z, zː} => {r, rː}
 {l, lː} => {r, rː}
 then: {rl, lr} => {rː, lː}

breathy-frication:
 [+breathy plosive] => [fricative -breathy]
 then: [labial fricative] => [labiodental]
 then: {ɣ, ɣː} => {j, jː}
 then: [+breathy !nasal] => [-breathy]

romanizer-old-sarvaran:
 unchanged

palatalisation-2:
 [alveolar fricative] => [postalveolar] / @sonorant&![nasal] _
 then: (
 [dental plosive +long] [front high -long] => [postalveolar affricate] [highmid] / _ $
 [alveolar fricative +long] [front high -long] => [postalveolar] [highmid] / _ $
 [alveolar !trill +long] [front high -long] => [palatal] [highmid] / _ $
 [dental plosive -long] => [postalveolar affricate] / _ [front high -long] $
 [alveolar fricative -long] => [postalveolar] / _ [front high -long] $
 [alveolar !trill -long] => [palatal] / _ [front high -long] $
 )

nasalisation-and-vowel-loss:
 @vowel* => [+nasalised] / _ [nasal]
 then: @vowel&[+long] => [-long] / @vowel&[+long] @consonant* _
 then: (
 {ji, wu} => {iː, uː} / @consonant _ $
 {j, w} @vowel&[-long low] => {e, o} * / @consonant _ $
 {j, w} @vowel&[-long highmid] => {i, u} * / @consonant _ $
 @vowel&[-long] => * / @vowel @continuant? @consonant _ $ // {$ @consonant? @continuant? @vowel&[-long] @consonant, @consonant&[+long], [approximant]} _
 [-breathy nasal -long] => * / @vowel _ // _ @vowel
 @vowel&[+long] => [-long] / _ $
 )
 then: (
 @vowel&[-long -nasalised] h => * / @vowel @continuant&![approximant] _ @vowel
 else: (
 @vowel&[-long -nasalised] => * / @vowel @continuant&![approximant] _ [fricative] @vowel // {$ @vowel []* _ [fricative] @vowel @continuant&![approximant], _ [fricative] @vowel {[plosive], [affricate]} $}
 @vowel&[-long] => * / @vowel @continuant&![approximant] _ [nasal] @vowel // $ @vowel []* _ @continuant&![approximant] @vowel @continuant&![approximant]
 )
 )
 then: h => * // @vowel _ @vowel
 then: [+breathy nasal] => [-breathy]
 then: {[+voiced velar]&@obstruent, j} => {ɯ, i} // {@vowel _, _ @vowel}
 then: [$voiced $Place fricative] {[plosive], [affricate]}&[$voiced $Place] => * [+long]

syllables:
 @consonant? [approximant]? [approximant]? {@vowel&[+syllabic] {@vowel&[-syllabic], {@vowel&[-syllabic]}? s? @continuant? @consonant}, @vowel&[+syllabic +long]} => [+heavy]
 s? @consonant? @continuant? {@vowel&[+syllabic] {@vowel&[-syllabic], {@vowel&[-syllabic]}? s? @continuant? @consonant}, @vowel&[+syllabic +long]} => [+heavy] / {$, @consonant*2} _
 @consonant? [approximant]? [approximant]? @vowel&[+syllabic] => [+heavy] / _ @consonant&[+long]
 @consonant? [approximant]? [approximant]? @vowel&[+syllabic] => [-heavy]
 s? @consonant? @continuant? @vowel&[+syllabic] => [+heavy] / {$, @consonant*2} _ @consonant&[+long]
 s? @consonant? @continuant? @vowel&[+syllabic] => [-heavy] / {$, @consonant*2} _

length-loss:
 {[approximant], [lateralapproximant]} => [-long]

stress defer:
 [] => [-stress notone]
 then: (
 <syl>&[-heavy] => [+stress] / _ <syl>&[-heavy] $
 else: <syl>&[+heavy] => [+stress] // {<syl>&[+heavy] []* _}
 else: <syl> => [+stress] / _ <syl> $
 else: <syl> => [+stress] / $ _
 )
 then propagate: (
 <syl>&[+heavy] => [+stress] / {<syl>&[+stress] {<syl>&[-stress]}*2 _, _ {<syl>&[-stress]}*2 <syl>&[+stress]} // _ $
 else: <syl> => [+stress] / {<syl>&[+stress] <syl>&[-stress] _, _ <syl>&[-stress] <syl>&[+stress]}
 )
 then: <syl> => [-stress] / _ $ // $ {<syl>&[-stress]}* _

stress-1:
 :stress

romanizer-middle-sarvaran:
 unchanged

long-vowel-shift:
 @vowel => [-long] / _ [nasal]
 then: * @vowel&[+long high] => a [-long -syllabic] // _ {@vowel, [fricative] .}
 then: (
 [front !high +long] * => [-long] j
 [back !high +long] *  => [-long] w
 [front !high +long] * {j, w} => [-long] j {e, u} // _ @vowel
 [back !high +long] * {j, w} => [-long] w {i, o} // _ @vowel
 )
 then: (
 [front !high +long] * {@vowel&[-syllabic]}? => [-long] i̯ *
 [back !high +long] * {@vowel&[-syllabic]}? => [-long] u̯ *
 )
 then: (
 @vowel&[front high] h => [low front -rounded] j / _ @vowel
 @vowel&[back high] h => [low front -rounded] w / _ @vowel
 h => j / @vowel&[front] _ @vowel
 h => w / @vowel&[back] _ @vowel
 )
 then: @vowel&[+long] => [-long]
 then: @vowel => [+nasalised] / [+nasalised] _
 then: h => *
 then: (
 []$1 $1 => [+long] *
 @vowel&[$Frontness $Height -long +syllabic]  @vowel&[$Frontness $Height -long -syllabic] => * [+long]
 [high $Frontness] => [-syllabic] / @vowel&[$Frontness] _ // @vowel*2 _
 )
 then: (
 {u̯, i̯} => {w, j} / _ @vowel
 {w, j} => {u̯, i̯} / @vowel _ // {@vowel*2 _, _ @vowel}
 )
 then: [+long] => [-long] / _ $

syllables:
 @consonant? [approximant]? {@vowel&[+syllabic] {@vowel&[-syllabic], {@vowel&[-syllabic]}? s? @continuant? @consonant}, @vowel&[+syllabic +long]} => [+heavy]
 s? @consonant? @continuant? {@vowel&[+syllabic] {@vowel&[-syllabic], {@vowel&[-syllabic]}? s? @continuant? @consonant}, @vowel&[+syllabic +long]} => [+heavy] / {$, @consonant*2} _
 @consonant? [approximant]? @vowel&[+syllabic] => [+heavy] / _ @consonant&[+long]
 @consonant? [approximant]? @vowel&[+syllabic] => [-heavy]
 s? @consonant? @continuant? @vowel&[+syllabic] => [+heavy] / {$, @consonant*2} _ @consonant&[+long]
 s? @consonant? @continuant? @vowel&[+syllabic] => [-heavy] / {$, @consonant*2} _

diphthong-shift:
 @vowel&[-syllabic] => [+syllabic]
 then: {i, u} => {j, w} / @vowel _ @vowel
 then: []$1 $1 => * [+long]
 then: {[high +rounded] [high -rounded], [high -rounded] [high +rounded]} => {* [+rounded +long], [+rounded +long] *}
 then: @vowel&[+syllabic] => [-syllabic] / @vowel _ // @vowel*2 _

elision:
 {@consonant @vowel}$1 $1 => {[+long] []} * // $ _

palatalisation-3:
 [velar plosive] => [postalveolar affricate] / _ {[palatal]}
 [velar fricative] => [postalveolar] / _ {[palatal]}
 then: (
 s [velar plosive] => * [postalveolar affricate] / @vowel _ @vowel
 s [velar fricative] => * [postalveolar] / @vowel _ @vowel
 )
 then: [palatal approximant] => * / {[palatal], [postalveolar]} _

approximantisation-2:
 @vowel => [+syllabic]
 then: {i, u} @vowel => {j, w} [hightone]
 then: {j, w} => [notone]
 then: @vowel => [-syllabic] / @vowel _ // @vowel*2 _
 then: @consonant&[+long] => [-long] / _ @continuant @vowel

assimilation:
 [alveolar nasal] => [$Place] / _ @consonant&[$Place]

romanizer-preclassical-sarvaran:
 unchanged

uvularisation:
 [velar -voiced] => [uvular]
 then: [front low] => [back] / [uvular] _
 then: @vowel* => [back] / _ {[uvular], [back], w}
 then: [back -rounded highmid] => [high] // _ [back high]

vowel-loss:
 @vowel&[-long] => * / @vowel @sonorant? @obstruent _ $ // $ @consonant @continuant? @vowel @continuant? @consonant _
 @vowel&[-long] => * / @vowel {@obstruent, [nasal]} _ @sonorant&![nasal] @vowel
 @obstruent @vowel&[-long] => [+voiced] * / @vowel _ $ // $ @consonant @continuant? @vowel @continuant? _
 @vowel&[+long] => [-long] / _ $

nasal-fortition:
 @vowel {m, n, ŋ, ɲ} => [+nasalised] {b, d, g, nd͡ʒ} / _ @continuant {@vowel, $}
 {m, n, ŋ, ɲ} => {mb, nd, ŋg, n̠d͡ʒ} / @vowel _ @continuant {@vowel, $}

vowel-shift:
 [highmid back] => [low front -rounded] / _ {w, [high back]}
 [low front] => [highmid] / _ {[palatal], [high front]}
 [highmid front] [high -syllabic] => [high] *
 then: (
 [low +nasalised] => [back highmid +rounded]
 [+nasalised] => [highmid]
 )
 then: [+nasalised] => [-nasalised]
 then: (
 [front high +rounded] => [-rounded]
 [back high -rounded] => [central]
 )
 then: (
 [low] @vowel&[-syllabic back +rounded] => [+long highmid back +rounded] *
 w [low] => * [back highmid +rounded]
 )

vowel-epenthesis:
 @vowel&[central high $rounded +syllabic] @sonorant * => [] [] u / _ $
 @vowel&[$Frontness $Height $rounded +syllabic] @sonorant * => [highmid] [] [$Frontness $Height $rounded +voiced +syllabic] / _ $
 then: [back highmid] => [+rounded]

debuccalisation:
 [-voiced fricative]&!{[alveolar], [postalveolar]} => h

h-shift:
 @vowel&[+syllabic] [fricative] => [+long] * // _ @vowel
 @obstruent h => [+aspirated] *
 then: h => * // {$, @vowel} _ @vowel

length-shift:
 @vowel&[+long -stress] => [-long] / {_ $, _ [+long], [+long] _}
 then: @consonant&[+long] => [-long] / {_ $, _ [+long], [+long] _}

voicing-shift:
 @obstruent => [-voiced] // {[+voiced], $} _ [+voiced]
 [-voiced plosive]&!{[uvular], [glottal]} => [+voiced] / @vowel _ @vowel

palatal-shift:
 [palatal approximant] => [postalveolar fricative]
 [alveolar lateralapproximant] => [palatal approximant] / {$, @consonant} _ @vowel
 [palatal lateralapproximant] => [palatal approximant] / {{$, @consonant} _ @vowel, @vowel _ {$, @consonant}}
 then: [palatal lateralapproximant] => [alveolar]
 then: [palatal] => [alveolar] / _ @vowel* [palatal]

romanizer-stonechild-imperial:
 unchanged

syllables:
 clear

romanizer:
 [+long]$1 => $1 $1
 then: [+long] => [-long]
 then: @vowel => [+syllabic]
 then: (
 [nasal $Place] => m / _ @consonant&[$Place]&{[labial], [labiodental]}
 [nasal $Place] => n / _ @consonant&[$Place]
 )
 then: {ng, ŋ} => {n'g, ng}
 then: [dental] => [alveolar]
 then: {ɑ́, ɑ, ǿ, ø, ɨ́, ɨ} => {á, a, ő, ö, i̋, ï}
 then: {d͡ʒ, t͡ʃ, ʒ, ʃ, ɲ, j} => {dj, tc, j, c, ny, y}