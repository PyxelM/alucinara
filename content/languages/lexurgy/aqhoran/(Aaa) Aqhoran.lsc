Feature +breathy, +rounded, +long, +syllabic
Feature Frontness(front, nearfront, central, nearback, back)
Feature Height(low, nearlow, lowmid, mid, highmid, nearhigh, high)
Feature Place(labial, labiodental, dental, alveolar, postalveolar, retroflex, alveopalatal, palatal, velar, uvular, pharyngeal, epiglottal, glottal)
Feature Manner(plosive, fricative, affricate, trill, flap, approximant, lateralapproximant, lateralfricative, lateralaffricate, nasal)
Feature +voiced, +labialised, +palatalised, +aspirated, +nasalised, +rhotic, +retracted, +prenasalised, +pharyngealised, +ejective, +breathyvoice, +noaudiblerelease
Feature Tone(*notone, lowtone, midtone, hightone)
Feature (syllable) +heavy
Feature (syllable) +stress

Diacritic ʱ [+breathy]
Diacritic ̤ [+breathyvoice]
Diacritic ̃ [+nasalised]
Diacritic ̥ [-voiced]
Diacritic ̬ [+voiced]
Diacritic ̹ [+rounded]
Diacritic ̜ [-rounded]
Diacritic ˈ (before) [+stress]
Diacritic ᴴ [+heavy]
Diacritic ̠ [+retracted]
Diacritic ̩ [+syllabic]
Diacritic ̯ [-syllabic]
Diacritic ́ (floating) [hightone]
Diacritic ̀ (floating) [lowtone]
Diacritic ̄ (floating) [midtone]
Diacritic ˞ [+rhotic]
Diacritic ʰ [+aspirated]
Diacritic ʷ [+labialised]
Diacritic ʲ [+palatalised]
Diacritic ː [+long]
Diacritic ⁿ (before) [+prenasalised]
Diacritic ˤ [+pharyngealised]
Diacritic ʼ [+ejective]
Diacritic ̚ [+noaudiblerelease]

Symbol i [front high -rounded +voiced +syllabic]
Symbol y [front high +rounded +voiced +syllabic]
Symbol ɪ [nearfront nearhigh -rounded +voiced +syllabic]
Symbol ʏ [nearfront nearhigh +rounded +voiced +syllabic]
Symbol e [front highmid -rounded +voiced +syllabic]
Symbol ø [front highmid +rounded +voiced +syllabic]
Symbol e̞ [front mid -rounded +voiced +syllabic]
Symbol ø̞ [front mid +rounded +voiced +syllabic]
Symbol ɛ [front lowmid -rounded +voiced +syllabic]
Symbol œ [front lowmid +rounded +voiced +syllabic]
Symbol a [front low -rounded +voiced +syllabic]
Symbol ɶ [front low +rounded +voiced +syllabic]
Symbol æ [front nearlow -rounded +voiced +syllabic]
Symbol ɨ [central high -rounded +voiced +syllabic]
Symbol ʉ [central high +rounded +voiced +syllabic]
Symbol ɘ [central highmid -rounded +voiced +syllabic]
Symbol ɵ [central highmid +rounded +voiced +syllabic]
Symbol ə [central mid -rounded +voiced +syllabic]
Symbol ɵ̞ [central mid +rounded +voiced +syllabic]
Symbol ɜ [central lowmid -rounded +voiced +syllabic]
Symbol ɞ [central lowmid +rounded +voiced +syllabic]
Symbol ä [central low -rounded +voiced +syllabic]
Symbol ɶ̈ [central low +rounded +voiced +syllabic]
Symbol ɐ [central nearlow -rounded +voiced +syllabic]
Symbol u [back high +rounded +voiced +syllabic]
Symbol ɯ [back high -rounded +voiced +syllabic]
Symbol ʊ [nearback nearhigh +rounded +voiced +syllabic]
Symbol ʊ̹ [nearback nearhigh -rounded +voiced +syllabic]
Symbol o [back highmid +rounded +voiced +syllabic]
Symbol ɤ [back highmid -rounded +voiced +syllabic]
Symbol o̞ [back mid +rounded +voiced +syllabic]
Symbol ɤ̞ [back mid -rounded +voiced +syllabic]
Symbol ɔ [back lowmid +rounded +voiced +syllabic]
Symbol ʌ [back lowmid -rounded +voiced +syllabic]
Symbol ɒ [back low +rounded +voiced +syllabic]
Symbol ɑ [back low -rounded +voiced +syllabic]

Symbol p [-voiced labial plosive -syllabic]
Symbol ɸ [-voiced labial fricative -syllabic]
Symbol p͡ɸ [-voiced labial affricate -syllabic]
Symbol b [+voiced labial plosive -syllabic]
Symbol ᵐb [+voiced labial plosive -syllabic +prenasalised]
Symbol β [+voiced labial fricative -syllabic]
Symbol b͡β [+voiced labial affricate -syllabic]
Symbol ʙ [+voiced labial trill -syllabic]
Symbol ʙ̆ [+voiced labial flap -syllabic]
Symbol m [+voiced labial nasal -syllabic]
Symbol β̞ [+voiced labial approximant -syllabic]

Symbol p̪ [-voiced labiodental plosive -syllabic]
Symbol f [-voiced labiodental fricative -syllabic]
Symbol p̪͡f [-voiced labiodental affricate -syllabic]
Symbol b̪ [+voiced labiodental plosive -syllabic]
Symbol v [+voiced labiodental fricative -syllabic]
Symbol b̪͡v [+voiced labiodental affricate -syllabic]
Symbol ʙ̠ [+voiced labiodental trill -syllabic]
Symbol ⱱ [+voiced labiodental flap -syllabic]
Symbol ɱ [+voiced labiodental nasal -syllabic]
Symbol ʋ [+voiced labiodental approximant -syllabic]

Symbol t [-voiced alveolar plosive -syllabic]
Symbol s [-voiced alveolar fricative -syllabic]
Symbol t͡s [-voiced alveolar affricate -syllabic]
Symbol d [+voiced alveolar plosive -syllabic]
Symbol z [+voiced alveolar fricative -syllabic]
Symbol d͡z [+voiced alveolar affricate -syllabic]
Symbol r [+voiced alveolar trill -syllabic]
Symbol ɾ [+voiced alveolar flap -syllabic]
Symbol n [+voiced alveolar nasal -syllabic]
Symbol ɹ [+voiced alveolar approximant -syllabic]
Symbol l [+voiced alveolar lateralapproximant -syllabic]
Symbol ɬ [-voiced alveolar lateralfricative -syllabic]
Symbol ɮ [+voiced alveolar lateralfricative -syllabic]
Symbol t͡ɬ [-voiced alveolar lateralaffricate -syllabic]
Symbol d͡ɮ [+voiced alveolar lateralaffricate -syllabic]

Symbol t̠ [-voiced postalveolar plosive -syllabic]
Symbol ʃ [-voiced postalveolar fricative -syllabic]
Symbol t͡ʃ [-voiced postalveolar affricate -syllabic]
Symbol d̠ [+voiced postalveolar plosive -syllabic]
Symbol ʒ [+voiced postalveolar fricative -syllabic]
Symbol d͡ʒ [+voiced postalveolar affricate -syllabic]
Symbol r̠ [+voiced postalveolar trill -syllabic]
Symbol ɾ̠ [+voiced postalveolar flap -syllabic]
Symbol n̠ [+voiced postalveolar nasal -syllabic]
Symbol ɹ̠ [+voiced postalveolar approximant -syllabic]
Symbol l̠ [+voiced postalveolar lateralapproximant -syllabic]

Symbol t̪ [-voiced dental plosive -syllabic]
Symbol θ [-voiced dental fricative -syllabic]
Symbol t͡θ [-voiced dental affricate -syllabic]
Symbol d̪ [+voiced dental plosive -syllabic]
Symbol ð [+voiced dental fricative -syllabic]
Symbol d͡ð [+voiced dental affricate -syllabic]
Symbol r̪ [+voiced dental trill -syllabic]
Symbol ɾ̪ [+voiced dental flap -syllabic]
Symbol n̪ [+voiced dental nasal -syllabic]
Symbol ð̞ [+voiced dental approximant -syllabic]
Symbol θ̞ [-voiced dental approximant -syllabic]
Symbol l̪ [+voiced dental lateralapproximant -syllabic]

Symbol ʈ [-voiced retroflex plosive -syllabic]
Symbol ʂ [-voiced retroflex fricative -syllabic]
Symbol t͡ʂ [-voiced retroflex affricate -syllabic]
Symbol ɖ [+voiced retroflex plosive -syllabic]
Symbol ʐ [+voiced retroflex fricative -syllabic]
Symbol d͡ʐ [+voiced retroflex affricate -syllabic]
Symbol ɽː [+voiced retroflex trill -syllabic]
Symbol ɽ [+voiced retroflex flap -syllabic]
Symbol ɳ [+voiced retroflex nasal -syllabic]
Symbol ɻ [+voiced retroflex approximant -syllabic]
Symbol ɭ [+voiced retroflex lateralapproximant -syllabic]

Symbol c̟ [-voiced alveopalatal plosive -syllabic]
Symbol ɕ [-voiced alveopalatal fricative -syllabic]
Symbol t͡ɕ [-voiced alveopalatal affricate -syllabic]
Symbol ɟ̟ [+voiced alveopalatal plosive -syllabic]
Symbol ʑ [+voiced alveopalatal fricative -syllabic]
Symbol d͡ʑ [+voiced alveopalatal affricate -syllabic]
Symbol ɟ̟̆ː [+voiced alveopalatal trill -syllabic]
Symbol ɟ̟̆ [+voiced alveopalatal flap -syllabic]
Symbol ɲ̟ [+voiced alveopalatal nasal -syllabic]
Symbol j̟ [+voiced alveopalatal approximant -syllabic]
Symbol ʎ̟ [+voiced alveopalatal lateralapproximant -syllabic]

Symbol c [-voiced palatal plosive -syllabic]
Symbol ç [-voiced palatal fricative -syllabic]
Symbol c͡ç [-voiced palatal affricate -syllabic]
Symbol ɟ [+voiced palatal plosive -syllabic]
Symbol ᶮɟ [+voiced +prenasalised palatal plosive -syllabic]
Symbol ʝ [+voiced palatal fricative -syllabic]
Symbol ɟ͡ʝ [+voiced palatal affricate -syllabic]
Symbol ɟ̆ː [+voiced palatal trill -syllabic]
Symbol ɟ̆ [+voiced palatal flap -syllabic]
Symbol ɲ [+voiced palatal nasal -syllabic]
Symbol j [+voiced palatal approximant -syllabic]
Symbol ɥ [+voiced palatal approximant +labialised -syllabic]
Symbol ʎ [+voiced palatal lateralapproximant -syllabic]

Symbol k [-voiced velar plosive -syllabic]
Symbol x [-voiced velar fricative -syllabic]
Symbol k͡x [-voiced velar affricate -syllabic]
Symbol g [+voiced velar plosive -syllabic]
Symbol ᵑg [+voiced velar plosive -syllabic +prenasalised]
Symbol ɣ [+voiced velar fricative -syllabic]
Symbol g͡ɣ [+voiced velar affricate -syllabic]
Symbol ŋ [+voiced velar nasal -syllabic]
Symbol ŋ̊ [-voiced velar nasal -syllabic]
Symbol ɰ [+voiced velar approximant -syllabic]
Symbol w [+voiced velar approximant +labialised -syllabic]
Symbol ʍ [-voiced velar approximant +labialised -syllabic]
Symbol ʟ [+voiced velar lateralapproximant -syllabic]

Symbol q [-voiced uvular plosive -syllabic]
Symbol χ [-voiced uvular fricative -syllabic]
Symbol q͡χ [-voiced uvular affricate -syllabic]
Symbol ɢ [+voiced uvular plosive -syllabic]
Symbol ʁ [+voiced uvular fricative -syllabic]
Symbol ɢ͡ʁ [+voiced uvular affricate -syllabic]
Symbol ʀ [+voiced uvular trill -syllabic]
Symbol ɢ̆ [+voiced uvular flap -syllabic]
Symbol ɴ [+voiced uvular nasal -syllabic]
Symbol ʁ̞ [+voiced uvular approximant -syllabic]
Symbol ʟ̠ [+voiced uvular lateralapproximant -syllabic]

Symbol ħ [-voiced pharyngeal fricative -syllabic]
Symbol ʕ [+voiced pharyngeal fricative -syllabic]

Symbol ʡ [-voiced epiglottal plosive -syllabic]
Symbol ʜ [-voiced epiglottal fricative -syllabic]
Symbol ʡ͡ʜ [-voiced epiglottal affricate -syllabic]
Symbol ʢ [+voiced epiglottal fricative -syllabic]

Symbol ʔ [glottal plosive -syllabic]
Symbol h [-voiced glottal fricative -syllabic]
Symbol ʔ͡h [-voiced glottal affricate -syllabic]
Symbol ɦ [+voiced glottal fricative -syllabic]

Element consonant {[plosive], [fricative], [affricate], [trill], [flap], [approximant], [lateralapproximant], [lateralfricative], [lateralaffricate], [nasal]}
Element vowel {[front], [nearfront], [central], [nearback], [back]}
Element obstruent {[affricate], [plosive], [fricative], [lateralfricative], [lateralaffricate]}
Element sonorant {[!affricate !plosive !fricative !lateralfricative !lateralaffricate]&@consonant}
Element continuant {@sonorant, [fricative], [lateralfricative]}
Element laryngeal {[glottal], [epiglottal], [pharyngeal]}
Element labial {[labiodental], [labial]}
Element lax @vowel&{[nearfront], [nearhigh], [nearlow central]}
Element nonlax @vowel&!{[nearfront], [nearhigh], [nearlow central]}

deromanisation:
 {nw, ng, ÿ} => {ŋʷ, ŋ, ɰ}
 {ḱ, ǵ, ń, y} => {kʲ, gʲ, ŋʲ, ɰʲ}
 [velar] w => [+labialised] *
 then: []$1 $1 => [+long] *
 then: [-voiced plosive] h => [+aspirated] *
 then: {h, '} => {ʕ, ʔ}

glomination:
 \- => *

lengthening:
 []$1 $1 => * [+long]

syllables:
 @consonant? @continuant? {@vowel&[+syllabic] @consonant, @vowel&[+syllabic +long]} => [+heavy]
 @consonant? @continuant? @vowel&[+syllabic] => [-heavy]

syllables:
 explicit

romanizer-early-proto-central:
 unchanged

dentalisation:
 [alveolar plosive] => [dental]

affrication:
 [+aspirated] => [-aspirated affricate]

approximant-shift:
 [velar +palatalised] => [palatal -palatalised]

vowel-shift:
 [high] [low] => [highmid] *
 [highmid] [low] => [low +long] *
 then: (
 [low +long] [high] => * [highmid +long]
 [low] [high] => * [highmid]
 )
 then propagate: @vowel&[$Frontness $Height] @vowel&[$Frontness $Height] => [+long] *
 then: (
 [front $Height] [back $Height] => [back] *
 [back $Height] [front $Height] => [front] *
 )
 then: (
 [back] @consonant* [$Frontness high +long] => [$Frontness] [] [-long] / _ $
 [low] @consonant* [$Frontness high +long] => [$Frontness highmid] [] [-long] / _ $
 [highmid] @consonant* [$Frontness high +long] => [$Frontness high] [] [-long] / _ $
 [high] @consonant* [$rounded high +long] => [$rounded] [] [-long] / _ $

 [back] @consonant [$Frontness high] => [$Frontness] [] * / _ $
 [low] @consonant [$Frontness high] => [$Frontness highmid] [] * / _ $
 [highmid] @consonant [$Frontness high] => [$Frontness high] [] * / _ $
 [high] @consonant [$rounded high] => [$rounded] [] * / _ $

 [back] => [$Frontness] / _ @consonant* [$Frontness high]
 [low] => [$Frontness highmid] / _ @consonant* [$Frontness high]
 [highmid] => [$Frontness high] / _ @consonant* [$Frontness high]
 [high] => [$rounded] / _ @consonant* [$rounded high]
 )
 then: (
 j [highmid front] => * [high]
 j [low] => * [highmid front]
 w [highmid back] => * [high]
 w [low] => * [highmid back]
 )
 then: [back] => [+rounded]
 then: [front low] => [back]

syllables:
 @consonant? @continuant? {@vowel&[+syllabic] @consonant, @vowel&[+syllabic +long], @vowel&[+syllabic] @vowel&[-syllabic] @consonant?} => [+heavy]
 @consonant? @continuant? @vowel&[+syllabic] => [-heavy]

romanizer-old-aqhoran:
 unchanged

frication:
 [affricate] => [fricative]

debuccalisation:
 [-voiced labiodental fricative] => [glottal]
 then: [glottal] => *
 then: [labial !plosive !nasal] => [labiodental]

dipthongisation:
 @vowel&[high] => [-syllabic] / @vowel&![high] _
 then: (
 [back high -syllabic +rounded] [velar approximant -labialised] => * [+labialised]
 [back high -syllabic -rounded] [velar approximant +labialised] => * [-labialised]
 )

stress defer:
 (
 <syl>&[+heavy] => [+stress] / $ {<syl>&[-heavy]}* _ // _ $
 else: <syl>&!@vowel => [+stress] / $ _
 else: <syl> => [+stress] / $ @vowel _
 )
 then: (
 <syl>&[+heavy] => [+stress] / <syl>&[+stress] {<syl>&[-heavy -stress]}*2 _ // _ $
 else: <syl> => [+stress] / <syl>&[+stress] <syl>&[-stress] _ // _ $
 )

stress-1:
 :stress

vowel-loss:
 @vowel => * / !$ @vowel @consonant _ $

labialisation:
 [velar +labialised] => [labial -labialised] // {[velar +labialised], [labial]} @vowel* _
 [velar +labialised] => [-labialised]
 then: [labial !plosive !nasal] => [labiodental]

metathesis:
 {@sonorant&![approximant]}$1 @vowel$2 {@vowel&[-syllabic]}? => $2 $1 [+syllabic] / @vowel @consonant _ // _ !@vowel

stress-2:
 [] => [-stress]
 then: :stress

vowel-shift-2:
 {a, {e, i}, {o, u}} => {ʕ, j, w} / _ @vowel // @consonant*2 _
 then: (
 [back $Height] j [back $Height]? => [front +long] * * // _ @vowel
 [back $Height] ʕ [back $Height]? => [low -rounded +long] * * // _ @vowel
 [back highmid] {ɰ, w} [back highmid]? => [+long high] * * // _ @vowel
 [back $Height] {ɰ, w} [back $Height]? => [+long] * * // _ @vowel

 [front low] j [front low]? => [-rounded highmid +long] * * // _ @vowel
 [front $Height] j [front $Height]? => [-rounded high +long] * * // _ @vowel
 [front high] ʕ [front high]? => [highmid -rounded +long] * * // _ @vowel
 [front $Height] ʕ [front $Height]? => [low -rounded +long] * * // _ @vowel
 [front low] {ɰ, w} [front low]? => [back highmid +rounded +long] * * // _ @vowel
 [front $Height] {ɰ, w} [front $Height]? => [back high +rounded +long] * * // _ @vowel

 [back $Height]? j [back $Height] => * * [front +long]
 [back $Height]? ʕ [back $Height] => * * [low -rounded +long]
 [back highmid]? {ɰ, w} [back highmid] => * * [+long high]
 [back $Height]? {ɰ, w} [back $Height] => * * [+long]

 [front low]? j [front low] => * * [-rounded highmid +long]
 [front $Height]? j [front $Height] => * * [-rounded high +long]
 [front high]? ʕ [front high] => * * [highmid -rounded +long]
 [front $Height]? ʕ [front $Height] => * * [low -rounded +long]
 [front low]? {ɰ, w} [front low] => * * [back highmid +rounded +long]
 [front $Height]? {ɰ, w} [front $Height] => * * [back high +rounded +long]
 )
 then: @vowel => [+syllabic]
 then: {a, {e, i}, {o, u}} => {ʕ, j, w} / _ @vowel // @consonant*2 _
 then: @vowel&[high] => [-syllabic] / @vowel _ // @vowel*2 _

syllables:
 @consonant? @continuant? {@vowel&[+syllabic] @consonant, @vowel&[+syllabic +long], @vowel&[+syllabic] @vowel&[-syllabic] @consonant?} => [+heavy]
 @consonant? @continuant? {@vowel&[+syllabic], @sonorant&[+syllabic]} => [-heavy]

romanizer-middle-aqhoran:
 unchanged

postalveolarisation:
 [+voiced palatal plosive] => [approximant] / @vowel _ @vowel
 [palatal plosive] => [postalveolar affricate]
 [palatal fricative] => [postalveolar]

consonant-dropping:
 [velar fricative] => * / _ w
 [postalveolar fricative] => * / _ j
 then: [+voiced fricative] => * / @vowel _ // _ @vowel

uvularisation:
 @obstruent&[velar -voiced] => [uvular]

approximant-dropping:
 [velar approximant -labialised] => *
 [lateralapproximant +syllabic] => * / @vowel @consonant _ @consonant? {$, @continuant? @vowel}

lengthening-2:
 @vowel&[$Frontness $Height] @vowel&[$Frontness $Height +stress] => [+long +stress] *
 @vowel&[$Frontness $Height] @vowel&[$Frontness $Height] => [+long] *

syllables:
 @consonant? @continuant? {@vowel&[+syllabic] @consonant, @vowel&[+syllabic +long] @consonant?, @vowel&[+syllabic] @vowel&[-syllabic] @consonant?} => [+heavy]
 @consonant? @continuant? {@vowel&[+syllabic], @sonorant&[+syllabic]} => [-heavy]

stress-3:
 [] => [-stress]
 then: :stress

de-syllabification:
 * @consonant&[+syllabic] => a [-syllabic]

gemination:
 []$1 $1 => [+long] *
 {[alveolar], [dental]}&[plosive] [postalveolar affricate] => * [+long]
 {[alveolar], [dental]}&[$Manner] [postalveolar $Manner] => * [+long]
 then: [+long] => [-long] / {[+long] {@vowel*, @consonant*} _, _ $}

romanizer-aqhoran:
 unchanged

stress-marking:
 @vowel&[+syllabic +stress] => [hightone]

syllables:
 clear

romanizer:
 @vowel => [+syllabic]
 @consonant&[+long]$1 => $1 $1
 then: @consonant&[+long] => [-long]
 then: (
 {ɑ́, ǿ, ý, ǿː, ýː, ɑ́ː, éː, íː, óː, úː} => {á, ő, ű, ȍ, ȕ, à, è, ì, ò, ù}
 {ɑ, ø, y, øː, yː, ɑː, eː, iː, oː, uː} => {a, ö, ü, ö̂, ü̂, â, ê, î, ô, û}
 {ŋŋ, d͡ʒd͡ʒ, t͡ʃt͡ʃ, ɸɸ, θθ, χχ, ʋʋ, ʃʃ} => {nng, jj, ttc, pph, tth, qqh, vvh, cch}
 {ɸ, θ, χ, ɰ, ʋ, ʕ, ʔ, ŋ} => {ph, th, qh, ÿ, vh, h, ', ng}
 {d͡ʒ, t͡ʃ, ʃ, ɲ, j} => {j, c, ch, ń, y}
 [dental] => [alveolar]
 )
 then: @consonant&[+long]$1 => $1 $1
 then: [+long] => [-long]