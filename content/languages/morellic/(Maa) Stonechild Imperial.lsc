Feature +breathy, +rounded, +long, +syllabic
Feature Frontness(front, nearfront, central, nearback, back)
Feature Height(low, nearlow, lowmid, mid, highmid, nearhigh, high)
Feature Place(labial, labiodental, dental, alveolar, postalveolar, retroflex, alveopalatal, palatal, velar, uvular, pharyngeal, epiglottal, glottal)
Feature Manner(plosive, fricative, affricate, trill, flap, approximant, lateralapproximant, lateralfricative, lateralaffricate, nasal)
Feature +voiced, +labialised, +palatalised, +aspirated, +nasalised, +rhotic, +retracted, +prenasalised, +pharyngealised, +ejective, +breathyvoice, +noaudiblerelease
Feature Tone(*notone, lowtone, midtone, hightone)
Feature (syllable) +heavy
Feature (syllable) +stress

Diacritic ʱ [+breathy]
Diacritic ̤ [+breathyvoice]
Diacritic ̃ [+nasalised]
Diacritic ̥ [-voiced]
Diacritic ̬ [+voiced]
Diacritic ̹ [+rounded]
Diacritic ̜ [-rounded]
Diacritic ˈ (before) [+stress]
Diacritic ᴴ [+heavy]
Diacritic ̠ [+retracted]
Diacritic ̩ [+syllabic]
Diacritic ̯ [-syllabic]
Diacritic ́ [hightone]
Diacritic ̀ [lowtone]
Diacritic ̄ [midtone]
Diacritic ˞ [+rhotic]
Diacritic ʰ [+aspirated]
Diacritic ʷ [+labialised]
Diacritic ʲ [+palatalised]
Diacritic ː [+long]
Diacritic ⁿ (before) [+prenasalised]
Diacritic ˤ [+pharyngealised]
Diacritic ʼ [+ejective]
Diacritic ̚ [+noaudiblerelease]

Symbol i [front high -rounded +voiced +syllabic]
Symbol y [front high +rounded +voiced +syllabic]
Symbol ɪ [nearfront nearhigh -rounded +voiced +syllabic]
Symbol ʏ [nearfront nearhigh +rounded +voiced +syllabic]
Symbol e [front highmid -rounded +voiced +syllabic]
Symbol ø [front highmid +rounded +voiced +syllabic]
Symbol e̞ [front mid -rounded +voiced +syllabic]
Symbol ø̞ [front mid +rounded +voiced +syllabic]
Symbol ɛ [front lowmid -rounded +voiced +syllabic]
Symbol œ [front lowmid +rounded +voiced +syllabic]
Symbol a [front low -rounded +voiced +syllabic]
Symbol ɶ [front low +rounded +voiced +syllabic]
Symbol æ [front nearlow -rounded +voiced +syllabic]
Symbol ɨ [central high -rounded +voiced +syllabic]
Symbol ʉ [central high +rounded +voiced +syllabic]
Symbol ɘ [central highmid -rounded +voiced +syllabic]
Symbol ɵ [central highmid +rounded +voiced +syllabic]
Symbol ə [central mid -rounded +voiced +syllabic]
Symbol ɵ̞ [central mid +rounded +voiced +syllabic]
Symbol ɜ [central lowmid -rounded +voiced +syllabic]
Symbol ɞ [central lowmid +rounded +voiced +syllabic]
Symbol ä [central low -rounded +voiced +syllabic]
Symbol ɶ̈ [central low +rounded +voiced +syllabic]
Symbol ɐ [central nearlow -rounded +voiced +syllabic]
Symbol u [back high +rounded +voiced +syllabic]
Symbol ɯ [back high -rounded +voiced +syllabic]
Symbol ʊ [nearback nearhigh +rounded +voiced +syllabic]
Symbol ʊ̹ [nearback nearhigh -rounded +voiced +syllabic]
Symbol o [back highmid +rounded +voiced +syllabic]
Symbol ɤ [back highmid -rounded +voiced +syllabic]
Symbol o̞ [back mid +rounded +voiced +syllabic]
Symbol ɤ̞ [back mid -rounded +voiced +syllabic]
Symbol ɔ [back lowmid +rounded +voiced +syllabic]
Symbol ʌ [back lowmid -rounded +voiced +syllabic]
Symbol ɒ [back low +rounded +voiced +syllabic]
Symbol ɑ [back low -rounded +voiced +syllabic]

Symbol p [-voiced labial plosive -syllabic]
Symbol ɸ [-voiced labial fricative -syllabic]
Symbol p͡ɸ [-voiced labial affricate -syllabic]
Symbol b [+voiced labial plosive -syllabic]
Symbol ᵐb [+voiced labial plosive -syllabic +prenasalised]
Symbol β [+voiced labial fricative -syllabic]
Symbol b͡β [+voiced labial affricate -syllabic]
Symbol ʙ [+voiced labial trill -syllabic]
Symbol ʙ̆ [+voiced labial flap -syllabic]
Symbol m [+voiced labial nasal -syllabic]
Symbol β̞ [+voiced labial approximant -syllabic]

Symbol p̪ [-voiced labiodental plosive -syllabic]
Symbol f [-voiced labiodental fricative -syllabic]
Symbol p̪͡f [-voiced labiodental affricate -syllabic]
Symbol b̪ [+voiced labiodental plosive -syllabic]
Symbol v [+voiced labiodental fricative -syllabic]
Symbol b̪͡v [+voiced labiodental affricate -syllabic]
Symbol ʙ̠ [+voiced labiodental trill -syllabic]
Symbol ⱱ [+voiced labiodental flap -syllabic]
Symbol ɱ [+voiced labiodental nasal -syllabic]
Symbol ʋ [+voiced labiodental approximant -syllabic]

Symbol t [-voiced alveolar plosive -syllabic]
Symbol s [-voiced alveolar fricative -syllabic]
Symbol t͡s [-voiced alveolar affricate -syllabic]
Symbol d [+voiced alveolar plosive -syllabic]
Symbol z [+voiced alveolar fricative -syllabic]
Symbol d͡z [+voiced alveolar affricate -syllabic]
Symbol r [+voiced alveolar trill -syllabic]
Symbol ɾ [+voiced alveolar flap -syllabic]
Symbol n [+voiced alveolar nasal -syllabic]
Symbol ɹ [+voiced alveolar approximant -syllabic]
Symbol l [+voiced alveolar lateralapproximant -syllabic]
Symbol ɬ [-voiced alveolar lateralfricative -syllabic]
Symbol ɮ [+voiced alveolar lateralfricative -syllabic]
Symbol t͡ɬ [-voiced alveolar lateralaffricate -syllabic]
Symbol d͡ɮ [+voiced alveolar lateralaffricate -syllabic]

Symbol t̠ [-voiced postalveolar plosive -syllabic]
Symbol ʃ [-voiced postalveolar fricative -syllabic]
Symbol t͡ʃ [-voiced postalveolar affricate -syllabic]
Symbol d̠ [+voiced postalveolar plosive -syllabic]
Symbol ʒ [+voiced postalveolar fricative -syllabic]
Symbol d͡ʒ [+voiced postalveolar affricate -syllabic]
Symbol r̠ [+voiced postalveolar trill -syllabic]
Symbol ɾ̠ [+voiced postalveolar flap -syllabic]
Symbol n̠ [+voiced postalveolar nasal -syllabic]
Symbol ɹ̠ [+voiced postalveolar approximant -syllabic]
Symbol l̠ [+voiced postalveolar lateralapproximant -syllabic]

Symbol t̪ [-voiced dental plosive -syllabic]
Symbol θ [-voiced dental fricative -syllabic]
Symbol t͡θ [-voiced dental affricate -syllabic]
Symbol d̪ [+voiced dental plosive -syllabic]
Symbol ð [+voiced dental fricative -syllabic]
Symbol d͡ð [+voiced dental affricate -syllabic]
Symbol r̪ [+voiced dental trill -syllabic]
Symbol ɾ̪ [+voiced dental flap -syllabic]
Symbol n̪ [+voiced dental nasal -syllabic]
Symbol ð̞ [+voiced dental approximant -syllabic]
Symbol θ̞ [-voiced dental approximant -syllabic]
Symbol l̪ [+voiced dental lateralapproximant -syllabic]

Symbol ʈ [-voiced retroflex plosive -syllabic]
Symbol ʂ [-voiced retroflex fricative -syllabic]
Symbol t͡ʂ [-voiced retroflex affricate -syllabic]
Symbol ɖ [+voiced retroflex plosive -syllabic]
Symbol ʐ [+voiced retroflex fricative -syllabic]
Symbol d͡ʐ [+voiced retroflex affricate -syllabic]
Symbol ɽː [+voiced retroflex trill -syllabic]
Symbol ɽ [+voiced retroflex flap -syllabic]
Symbol ɳ [+voiced retroflex nasal -syllabic]
Symbol ɻ [+voiced retroflex approximant -syllabic]
Symbol ɭ [+voiced retroflex lateralapproximant -syllabic]

Symbol c̟ [-voiced alveopalatal plosive -syllabic]
Symbol ɕ [-voiced alveopalatal fricative -syllabic]
Symbol t͡ɕ [-voiced alveopalatal affricate -syllabic]
Symbol ɟ̟ [+voiced alveopalatal plosive -syllabic]
Symbol ʑ [+voiced alveopalatal fricative -syllabic]
Symbol d͡ʑ [+voiced alveopalatal affricate -syllabic]
Symbol ɟ̟̆ː [+voiced alveopalatal trill -syllabic]
Symbol ɟ̟̆ [+voiced alveopalatal flap -syllabic]
Symbol ɲ̟ [+voiced alveopalatal nasal -syllabic]
Symbol j̟ [+voiced alveopalatal approximant -syllabic]
Symbol ʎ̟ [+voiced alveopalatal lateralapproximant -syllabic]

Symbol c [-voiced palatal plosive -syllabic]
Symbol ç [-voiced palatal fricative -syllabic]
Symbol c͡ç [-voiced palatal affricate -syllabic]
Symbol ɟ [+voiced palatal plosive -syllabic]
Symbol ᶮɟ [+voiced +prenasalised palatal plosive -syllabic]
Symbol ʝ [+voiced palatal fricative -syllabic]
Symbol ɟ͡ʝ [+voiced palatal affricate -syllabic]
Symbol ɟ̆ː [+voiced palatal trill -syllabic]
Symbol ɟ̆ [+voiced palatal flap -syllabic]
Symbol ɲ [+voiced palatal nasal -syllabic]
Symbol j [+voiced palatal approximant -syllabic]
Symbol ɥ [+voiced palatal approximant +labialised -syllabic]
Symbol ʎ [+voiced palatal lateralapproximant -syllabic]

Symbol k [-voiced velar plosive -syllabic]
Symbol x [-voiced velar fricative -syllabic]
Symbol k͡x [-voiced velar affricate -syllabic]
Symbol g [+voiced velar plosive -syllabic]
Symbol ᵑg [+voiced velar plosive -syllabic +prenasalised]
Symbol ɣ [+voiced velar fricative -syllabic]
Symbol g͡ɣ [+voiced velar affricate -syllabic]
Symbol ŋ [+voiced velar nasal -syllabic]
Symbol ŋ̊ [-voiced velar nasal -syllabic]
Symbol ɰ [+voiced velar approximant -syllabic]
Symbol w [+voiced velar approximant +labialised -syllabic]
Symbol ʍ [-voiced velar approximant +labialised -syllabic]
Symbol ʟ [+voiced velar lateralapproximant -syllabic]

Symbol q [-voiced uvular plosive -syllabic]
Symbol χ [-voiced uvular fricative -syllabic]
Symbol q͡χ [-voiced uvular affricate -syllabic]
Symbol ɢ [+voiced uvular plosive -syllabic]
Symbol ʁ [+voiced uvular fricative -syllabic]
Symbol ɢ͡ʁ [+voiced uvular affricate -syllabic]
Symbol ʀ [+voiced uvular trill -syllabic]
Symbol ɢ̆ [+voiced uvular flap -syllabic]
Symbol ɴ [+voiced uvular nasal -syllabic]
Symbol ʁ̞ [+voiced uvular approximant -syllabic]
Symbol ʟ̠ [+voiced uvular lateralapproximant -syllabic]

Symbol ħ [-voiced pharyngeal fricative -syllabic]
Symbol ʕ [+voiced pharyngeal fricative -syllabic]

Symbol ʡ [-voiced epiglottal plosive -syllabic]
Symbol ʜ [-voiced epiglottal fricative -syllabic]
Symbol ʡ͡ʜ [-voiced epiglottal affricate -syllabic]
Symbol ʢ [+voiced epiglottal fricative -syllabic]

Symbol ʔ [glottal plosive -syllabic]
Symbol h [-voiced glottal fricative -syllabic]
Symbol ʔ͡h [-voiced glottal affricate -syllabic]
Symbol ɦ [+voiced glottal fricative -syllabic]

Element consonant {[plosive], [fricative], [affricate], [trill], [flap], [approximant], [lateralapproximant], [lateralfricative], [lateralaffricate], [nasal]}
Element vowel {[front], [nearfront], [central], [nearback], [back]}
Element obstruent {[affricate], [plosive], [fricative], [lateralfricative], [lateralaffricate]}
Element sonorant {[!affricate !plosive !fricative !lateralfricative !lateralaffricate]&@consonant}
Element continuant {@sonorant, [fricative], [lateralfricative]}
Element laryngeal {[glottal], [epiglottal], [pharyngeal]}
Element labial {[labiodental], [labial]}
Element lax @vowel&{[nearfront], [nearhigh], [nearlow central]}
Element nonlax @vowel&!{[nearfront], [nearhigh], [nearlow central]}

deromanisation:
 {ng} => {ŋ}
 then: (
 {[plosive +voiced], [nasal], [lateralapproximant]} h => [+breathy] *
 {y, h} => {ɣ, x}
 {{â, â}, {ê, ê}, {î, î}, {ô, ô}, {û, û}, ï̂, ü̂} => {aː, eː, iː, oː, uː, ɯː, yː}
 {ï, ü} => {ɯ, y}
 \' => *
 )

contraction:
 \-ty => * / _ {\-vaː, \-ogʱ}? \-ilʱ

glomination:
 \- ə / @vowel _ => *
 @vowel \- / _ @vowel => *
 \- => *

assimilation:
 [nasal] => [$Place] / _ [$Place]&@consonant // {$, @consonant} _ @consonant @vowel
 then: [nasal] [lateralapproximant] => * [+long] / @vowel _ @vowel
 then: [labiodental nasal] => [labial]
 then: {[nasal], [lateralapproximant], [plosive]}&[+voiced] => [+breathy] / _ @continuant&[+breathy]
 then: @obstruent => [-voiced] / _ @continuant&[-voiced]

diphthongisation defer:
 []$1 $1 => [+long] *
 [high $Frontness] => [-syllabic] / @vowel&[$Frontness] _ // @vowel*2 _
 then: {u̯, i̯} => {w, j} / _ @vowel

diphthongisation:
 :diphthongisation

syllables:
 s? @consonant? @continuant? {@vowel&[+syllabic] {@vowel&[-syllabic], {@vowel&[-syllabic]}? {[nasal], [lateralapproximant], [trill], [flap]}? @consonant}, @vowel&[+syllabic +long]} => [+heavy]
 s? @consonant? @continuant? @vowel&[+syllabic] => [-heavy]

syllables:
 explicit

romanizer-early-proto-central:
 unchanged

dentalisation:
 [alveolar]&!@continuant => [dental]

frication:
 s [+breathy] => * [-voiced] / . _
 then: [+breathy plosive] => [fricative -breathy] // !@vowel _ @continuant&![nasal] @vowel

syllables:
 s? @consonant? @continuant? {@vowel&[+syllabic] {@vowel&[-syllabic], {@vowel&[-syllabic]}? {[nasal], [lateralapproximant], [trill], [flap]}? @consonant}, @vowel&[+syllabic +long]} => [+heavy]
 s? @consonant? @continuant? @vowel&[+syllabic] => [-heavy]

approximantisation:
 {v, vː} => {w, wː}
 {ŋɣ, ɣ, ɣː} => {ɲ, j, jː}
 then: {{z, l}, {zː, lː}} => {r, rː} // _ j
 then: {xw, jw / @vowel _ @vowel, jw} => {f, jv, gw}

palatalisation:
 [plosive dental] j => [postalveolar affricate] *
 [fricative alveolar] j => [postalveolar] *
 @sonorant&[alveolar !trill] j => [palatal] *

syllables:
 s? @consonant? @continuant? {@vowel&[+syllabic] {@vowel&[-syllabic], {@vowel&[-syllabic]}? {[nasal], [lateralapproximant], [trill], [flap]}? @consonant}, @vowel&[+syllabic +long]} => [+heavy]
 s? @consonant? @continuant? @vowel&[+syllabic] => [-heavy]

labiodentalisation:
 [labial fricative] => [labiodental]

breathy-tenuisation:
 [+breathy !nasal] => [-breathy]

lateral-assimilation:
 {rl, lr} => {rː, lː}

romanizer-old-sarvaran:
 unchanged

nasalisation-and-vowel-loss:
 @vowel* => [+nasalised] / _ [nasal]
 then: (
 [-breathy nasal -long $Place] => * / @vowel _ {$, ., @consonant} // _ [$Place plosive]
 @vowel&[-long] => * / @vowel @continuant? @consonant _ $ // {$ {@consonant*}? @vowel @consonant&!{[+long], ɲ, ʎ, [affricate], @sonorant}, @obstruent @sonorant} _
 @vowel&[+long] => [-long] / _ $
 )
 then: (
 i => * / @vowel @consonant _ j @vowel
 u => * / @vowel @consonant _ w @vowel
 )
 then: [+breathy nasal] => [-breathy]

syllables:
 s? @consonant? @continuant? {@vowel&[+syllabic] {@vowel&[-syllabic], {@vowel&[-syllabic]}? s? @continuant? @consonant}, @vowel&[+syllabic +long]} => [+heavy]
 s? @consonant? @continuant? @vowel&[+syllabic] => [-heavy]

long-vowel-shift:
 * @vowel&[+long high] => a [-long -syllabic] // _ {@vowel, [fricative] .}
 then: [front !high +long] * => [-long highmid] i̯
 then: [back !high +long] * => [-long highmid] u̯
 then: @vowel&[+long] => [-long]
 then: @vowel => [+nasalised] / [+nasalised] _
 then: [+long] => [-long] / _ $

diphthong-shift:
 @vowel&[-syllabic] => [+syllabic]
 then: {j, w} => {i, u} / @vowel _ // _ @vowel
 then: {i, u} => {j, w} / @vowel _ @vowel
 then: []$1 $1 => * [+long]
 then: {[high +rounded] [high -rounded], [high -rounded] [high +rounded]} => {* [+rounded +long], [+rounded +long] *}
 then: @vowel&[+syllabic] => [-syllabic] / @vowel _ // @vowel*2 _

epenthesis-2:
 * => $1 / {@vowel&[+syllabic]}$1 {[]*}? [alveolar trill] _ // _ {@sonorant? @vowel, {<syl>+}? $}

frication-and-stopping:
 {[plosive], [affricate]} => [fricative] / @vowel @sonorant*(1-2) _ // {[nasal] _, _ {@vowel, [plosive], [affricate]}}
 {[plosive], [affricate]} => [fricative] / @vowel @sonorant*(0-2) _ {$, .} // {[nasal] _, _ {[plosive], [affricate], @sonorant}}
 then: [fricative] => [plosive] / _ @sonorant&![nasal] @vowel
 then: [labiodental plosive] => [labial]

metathesis:
 {@sonorant&!{[trill], [approximant]}}$1 {j, w}$2 => $2 $1
 then: {j, w} => {i, u} / {$, @consonant} _ @consonant

elision:
 {@consonant @vowel}$1 $1 => {[+long] []} * // $ _

syllables:
 s? @consonant? @continuant? {@vowel&[+syllabic] {@vowel&[-syllabic], {@vowel&[-syllabic]}? s? @continuant? @consonant}, @vowel&[+syllabic +long]} => [+heavy]
 s? @consonant? @continuant? @vowel&[+syllabic] => [-heavy]

stress:
 (
 <syl>&[+heavy] => [+stress] // _ []* <syl>&[+heavy]
 else: <syl> => [+stress] / $ _
 )
 then propagate: <syl> => [+stress] / _ <syl>&[-stress] <syl>&[+stress]
 then: <syl> => [-stress] / [+stress] {[]*}? _ $
 then: @vowel&[+stress +syllabic] => [hightone]

unstressed-vowel-shift:
 {a, i, u, y, ɯ}&[-stress -long] => {ɐ, ɪ, ʊ, ʏ, ə} // _ $
 {ã, ĩ, ũ, ỹ, ɯ̃}&[-stress -long] => {ɐ̃, ɪ̃, ʊ̃, ʏ̃, ə̃} // _ $

romanizer-sarvaran:
 unchanged

unstressed-vowel-shift-undo:
 {ɐ, ɪ, ʊ, ʏ, ə} => {a, i, u, y, ɯ}
 {ɐ̃, ɪ̃, ʊ̃, ʏ̃, ə̃} => {ã, ĩ, ũ, ỹ, ɯ̃}

fricative-dropping:
 [dental fricative] => [plosive]
 [fricative !alveolar] => * / @vowel _ // _ @vowel

lateral-and-palatal-shift:
 [palatal lateralapproximant] => [approximant]
 then: [alveolar plosive] [lateralapproximant] => [lateralaffricate -voiced] *
 then: (
 [alveolar lateralapproximant +long] => [lateralfricative -voiced -long]
 [alveolar lateralapproximant] => [lateralfricative -voiced] // _ @vowel
 then: [alveolar trill] => [-voiced] // _ @vowel
 )
 then: (
 {i̯j, u̯w} => {jː, wː}
 {i̯, u̯} => {j, w} // _ {@consonant*2 {$, ., @consonant}, @consonant $}
 then: {ij, uw} => {ej, ow}
 then: (
 {j, jː, w, wː} => {ʒ, ʒː, v, vː} / @vowel _ // _ @continuant {$, @consonant}
 )
 )

sibilant-cluster-simplification:
 {sp, st̪, {{s, z, ʃ, ʒ} k, {s, z, ʃ, ʒ} t͡ʃ}} => {f, s, ʃ}
 {sb, sd̪, {{s, z, ʃ, ʒ} g, {s, z, ʃ, ʒ} d͡ʒ}} => {v, z, ʒ}

fricative-debuccalisation:
 [-voiced !alveolar !postalveolar fricative] => h
 then: @vowel h => [+long] * // _ @vowel
 then: h => * // {@vowel, $} _ @vowel

uvularisation:
 @obstruent&[velar] w => [uvular] *
 then: (
 [-voiced uvular plosive] => [fricative]
 [+voiced plosive]&{[velar], [uvular]} => [-voiced uvular]
 )
 then: [velar nasal] => [uvular] / _ [uvular]

approximant-loss:
 {j // @vowel _ @vowel, i̯} => *
 {w // @vowel _ @vowel, v} => {*, w}

voicing-assimilation:
 @obstruent => [$voiced] / _ @consonant&[$voiced]
 @obstruent => [-voiced] / _ $
 [nasal $Place] @obstruent&[-voiced] => [-voiced] *
 then: [uvular nasal] => [velar] // _ [uvular]

denasalisation:
 @vowel&[+nasalised] => [highmid -nasalised]

vowel-shift:
 [front +rounded] => [-rounded]
 [back -rounded] => [+rounded]
 then: [high +syllabic] => [highmid] / [high] @consonant* _
 then: e => a / _ @consonant* a $
 then: @vowel => [highmid] / _ $
 then: @vowel&[$Frontness $Height] h @vowel&[$Frontness $Height] => [+long] * *
 then: [highmid] => [low] / {[uvular] _, _ [uvular]}

romanizer-stonechild-imperial:
 unchanged

syllables:
 clear

romanizer:
 @vowel&[+long] * => [-long] ̂
 @consonant&[+long]$1 => $1 $1
 then: [+long] => [-long]
 then: @vowel => [+syllabic]
 then: (
 [nasal $Place] => m / _ @consonant&[$Place]&{[labial], [labiodental]}
 [nasal $Place] => n / _ @consonant&[$Place]
 )
 then: (
 {ng, ŋ} => {n'g, ng}
 {[labial], [dental], [velar]}&[fricative] * => [plosive] h
 )
 then: [dental] => [alveolar]
 then: (
 @vowel&[+nasalised] => [-nasalised] / _ {[+nasalised], [nasal]}
 @vowel&[+nasalised] * => [-nasalised] ̨
 )
 then: {ɐ, ɪ, ʊ, ŷ, {y, ʏ}, ɯ̂, {ɯ, ə}, ý, ɯ́} => {a, i, u, ü̂, ü, ï̂, ï, ű, i̋}
 then: {d͡ʒ, t͡ʃ, ʒ, ʃ, ɲ, ʎ, j} => {dj, tc, j, c, ny, ll, y}