Feature +breathy, +rounded, +long, +syllabic
Feature Frontness(front, nearfront, central, nearback, back)
Feature Height(low, nearlow, lowmid, mid, highmid, nearhigh, high)
Feature Place(labial, labiodental, dental, alveolar, postalveolar, retroflex, alveopalatal, palatal, velar, uvular, pharyngeal, epiglottal, glottal)
Feature Manner(plosive, fricative, affricate, implosive, trill, flap, approximant, lateralapproximant, lateralfricative, lateralaffricate, nasal)
Feature +voiced, +labialised, +palatalised, +aspirated, +nasalised, +rhotic, +retracted, +prenasalised, +pharyngealised, +ejective, +breathyvoice, +noaudiblerelease
Feature Tone(*notone, lowtone, midtone, hightone)
Feature (syllable) +heavy
Feature (syllable) +stress

Diacritic ʱ [+breathy]
Diacritic ̤ [+breathyvoice]
Diacritic ̃ [+nasalised]
Diacritic ̥ [-voiced]
Diacritic ̬ [+voiced]
Diacritic ̹ [+rounded]
Diacritic ̜ [-rounded]
Diacritic ˈ (before) [+stress]
Diacritic ᴴ [+heavy]
Diacritic ̠ [+retracted]
Diacritic ̩ [+syllabic]
Diacritic ̯ [-syllabic]
Diacritic ́ (floating) [hightone]
Diacritic ̀ (floating) [lowtone]
Diacritic ̄ (floating) [midtone]
Diacritic ˞ [+rhotic]
Diacritic ʰ [+aspirated]
Diacritic ʷ [+labialised]
Diacritic ʲ [+palatalised]
Diacritic ː [+long]
Diacritic ⁿ (before) [+prenasalised]
Diacritic ˤ [+pharyngealised]
Diacritic ʼ [+ejective]
Diacritic ̚ [+noaudiblerelease]

Symbol i [front high -rounded +voiced +syllabic]
Symbol y [front high +rounded +voiced +syllabic]
Symbol ɪ [nearfront nearhigh -rounded +voiced +syllabic]
Symbol ʏ [nearfront nearhigh +rounded +voiced +syllabic]
Symbol e [front highmid -rounded +voiced +syllabic]
Symbol ø [front highmid +rounded +voiced +syllabic]
Symbol e̞ [front mid -rounded +voiced +syllabic]
Symbol ø̞ [front mid +rounded +voiced +syllabic]
Symbol ɛ [front lowmid -rounded +voiced +syllabic]
Symbol œ [front lowmid +rounded +voiced +syllabic]
Symbol a [front low -rounded +voiced +syllabic]
Symbol ɶ [front low +rounded +voiced +syllabic]
Symbol æ [front nearlow -rounded +voiced +syllabic]
Symbol ɨ [central high -rounded +voiced +syllabic]
Symbol ʉ [central high +rounded +voiced +syllabic]
Symbol ɘ [central highmid -rounded +voiced +syllabic]
Symbol ɵ [central highmid +rounded +voiced +syllabic]
Symbol ə [central mid -rounded +voiced +syllabic]
Symbol ɵ̞ [central mid +rounded +voiced +syllabic]
Symbol ɜ [central lowmid -rounded +voiced +syllabic]
Symbol ɞ [central lowmid +rounded +voiced +syllabic]
Symbol ä [central low -rounded +voiced +syllabic]
Symbol ɶ̈ [central low +rounded +voiced +syllabic]
Symbol ɐ [central nearlow -rounded +voiced +syllabic]
Symbol u [back high +rounded +voiced +syllabic]
Symbol ɯ [back high -rounded +voiced +syllabic]
Symbol ʊ [nearback nearhigh +rounded +voiced +syllabic]
Symbol ʊ̹ [nearback nearhigh -rounded +voiced +syllabic]
Symbol o [back highmid +rounded +voiced +syllabic]
Symbol ɤ [back highmid -rounded +voiced +syllabic]
Symbol o̞ [back mid +rounded +voiced +syllabic]
Symbol ɤ̞ [back mid -rounded +voiced +syllabic]
Symbol ɔ [back lowmid +rounded +voiced +syllabic]
Symbol ʌ [back lowmid -rounded +voiced +syllabic]
Symbol ɒ [back low +rounded +voiced +syllabic]
Symbol ɑ [back low -rounded +voiced +syllabic]

Symbol p [-voiced labial plosive -syllabic]
Symbol ɸ [-voiced labial fricative -syllabic]
Symbol p͡ɸ [-voiced labial affricate -syllabic]
Symbol b [+voiced labial plosive -syllabic]
Symbol ɓ [+voiced labial implosive -syllabic]
Symbol ᵐb [+voiced labial plosive -syllabic +prenasalised]
Symbol β [+voiced labial fricative -syllabic]
Symbol b͡β [+voiced labial affricate -syllabic]
Symbol ʙ [+voiced labial trill -syllabic]
Symbol ʙ̆ [+voiced labial flap -syllabic]
Symbol m [+voiced labial nasal -syllabic]
Symbol β̞ [+voiced labial approximant -syllabic]

Symbol p̪ [-voiced labiodental plosive -syllabic]
Symbol f [-voiced labiodental fricative -syllabic]
Symbol p̪͡f [-voiced labiodental affricate -syllabic]
Symbol b̪ [+voiced labiodental plosive -syllabic]
Symbol ɓ̪ [+voiced labiodental implosive -syllabic]
Symbol v [+voiced labiodental fricative -syllabic]
Symbol b̪͡v [+voiced labiodental affricate -syllabic]
Symbol ʙ̠ [+voiced labiodental trill -syllabic]
Symbol ⱱ [+voiced labiodental flap -syllabic]
Symbol ɱ [+voiced labiodental nasal -syllabic]
Symbol ʋ [+voiced labiodental approximant -syllabic]

Symbol t [-voiced alveolar plosive -syllabic]
Symbol s [-voiced alveolar fricative -syllabic]
Symbol t͡s [-voiced alveolar affricate -syllabic]
Symbol d [+voiced alveolar plosive -syllabic]
Symbol ɗ [+voiced alveolar implosive -syllabic]
Symbol z [+voiced alveolar fricative -syllabic]
Symbol d͡z [+voiced alveolar affricate -syllabic]
Symbol r [+voiced alveolar trill -syllabic]
Symbol ɾ [+voiced alveolar flap -syllabic]
Symbol n [+voiced alveolar nasal -syllabic]
Symbol ɹ [+voiced alveolar approximant -syllabic]
Symbol l [+voiced alveolar lateralapproximant -syllabic]
Symbol ɬ [-voiced alveolar lateralfricative -syllabic]
Symbol ɮ [+voiced alveolar lateralfricative -syllabic]
Symbol t͡ɬ [-voiced alveolar lateralaffricate -syllabic]
Symbol d͡ɮ [+voiced alveolar lateralaffricate -syllabic]

Symbol t̠ [-voiced postalveolar plosive -syllabic]
Symbol ʃ [-voiced postalveolar fricative -syllabic]
Symbol t͡ʃ [-voiced postalveolar affricate -syllabic]
Symbol d̠ [+voiced postalveolar plosive -syllabic]
Symbol ɗ̠ [+voiced postalveolar implosive -syllabic]
Symbol ʒ [+voiced postalveolar fricative -syllabic]
Symbol d͡ʒ [+voiced postalveolar affricate -syllabic]
Symbol r̠ [+voiced postalveolar trill -syllabic]
Symbol ɾ̠ [+voiced postalveolar flap -syllabic]
Symbol n̠ [+voiced postalveolar nasal -syllabic]
Symbol ɹ̠ [+voiced postalveolar approximant -syllabic]
Symbol l̠ [+voiced postalveolar lateralapproximant -syllabic]

Symbol t̪ [-voiced dental plosive -syllabic]
Symbol θ [-voiced dental fricative -syllabic]
Symbol t͡θ [-voiced dental affricate -syllabic]
Symbol d̪ [+voiced dental plosive -syllabic]
Symbol ɗ̪ [+voiced dental implosive -syllabic]
Symbol ð [+voiced dental fricative -syllabic]
Symbol d͡ð [+voiced dental affricate -syllabic]
Symbol r̪ [+voiced dental trill -syllabic]
Symbol ɾ̪ [+voiced dental flap -syllabic]
Symbol n̪ [+voiced dental nasal -syllabic]
Symbol ð̞ [+voiced dental approximant -syllabic]
Symbol θ̞ [-voiced dental approximant -syllabic]
Symbol l̪ [+voiced dental lateralapproximant -syllabic]

Symbol ʈ [-voiced retroflex plosive -syllabic]
Symbol ʂ [-voiced retroflex fricative -syllabic]
Symbol t͡ʂ [-voiced retroflex affricate -syllabic]
Symbol ɖ [+voiced retroflex plosive -syllabic]
Symbol ᶑ [+voiced retroflex implosive -syllabic]
Symbol ʐ [+voiced retroflex fricative -syllabic]
Symbol d͡ʐ [+voiced retroflex affricate -syllabic]
Symbol ɽː [+voiced retroflex trill -syllabic]
Symbol ɽ [+voiced retroflex flap -syllabic]
Symbol ɳ [+voiced retroflex nasal -syllabic]
Symbol ɻ [+voiced retroflex approximant -syllabic]
Symbol ɭ [+voiced retroflex lateralapproximant -syllabic]

Symbol c̟ [-voiced alveopalatal plosive -syllabic]
Symbol ɕ [-voiced alveopalatal fricative -syllabic]
Symbol t͡ɕ [-voiced alveopalatal affricate -syllabic]
Symbol ɟ̟ [+voiced alveopalatal plosive -syllabic]
Symbol ʄ̟ [+voiced alveopalatal implosive -syllabic]
Symbol ʑ [+voiced alveopalatal fricative -syllabic]
Symbol d͡ʑ [+voiced alveopalatal affricate -syllabic]
Symbol ɟ̟̆ː [+voiced alveopalatal trill -syllabic]
Symbol ɟ̟̆ [+voiced alveopalatal flap -syllabic]
Symbol ɲ̟ [+voiced alveopalatal nasal -syllabic]
Symbol j̟ [+voiced alveopalatal approximant -syllabic]
Symbol ʎ̟ [+voiced alveopalatal lateralapproximant -syllabic]

Symbol c [-voiced palatal plosive -syllabic]
Symbol ç [-voiced palatal fricative -syllabic]
Symbol c͡ç [-voiced palatal affricate -syllabic]
Symbol ɟ [+voiced palatal plosive -syllabic]
Symbol ʄ [+voiced palatal implosive -syllabic]
Symbol ᶮɟ [+voiced +prenasalised palatal plosive -syllabic]
Symbol ʝ [+voiced palatal fricative -syllabic]
Symbol ɟ͡ʝ [+voiced palatal affricate -syllabic]
Symbol ɟ̆ː [+voiced palatal trill -syllabic]
Symbol ɟ̆ [+voiced palatal flap -syllabic]
Symbol ɲ [+voiced palatal nasal -syllabic]
Symbol j [+voiced palatal approximant -syllabic]
Symbol ɥ [+voiced palatal approximant +labialised -syllabic]
Symbol ʎ [+voiced palatal lateralapproximant -syllabic]

Symbol k [-voiced velar plosive -syllabic]
Symbol x [-voiced velar fricative -syllabic]
Symbol k͡x [-voiced velar affricate -syllabic]
Symbol g [+voiced velar plosive -syllabic]
Symbol ɠ [+voiced velar implosive -syllabic]
Symbol ᵑg [+voiced velar plosive -syllabic +prenasalised]
Symbol ɣ [+voiced velar fricative -syllabic]
Symbol g͡ɣ [+voiced velar affricate -syllabic]
Symbol ŋ [+voiced velar nasal -syllabic]
Symbol ŋ̊ [-voiced velar nasal -syllabic]
Symbol ɰ [+voiced velar approximant -syllabic]
Symbol w [+voiced velar approximant +labialised -syllabic]
Symbol ʍ [-voiced velar approximant +labialised -syllabic]
Symbol ʟ [+voiced velar lateralapproximant -syllabic]

Symbol q [-voiced uvular plosive -syllabic]
Symbol χ [-voiced uvular fricative -syllabic]
Symbol q͡χ [-voiced uvular affricate -syllabic]
Symbol ɢ [+voiced uvular plosive -syllabic]
Symbol ʛ [+voiced uvular implosive -syllabic]
Symbol ʁ [+voiced uvular fricative -syllabic]
Symbol ɢ͡ʁ [+voiced uvular affricate -syllabic]
Symbol ʀ [+voiced uvular trill -syllabic]
Symbol ɢ̆ [+voiced uvular flap -syllabic]
Symbol ɴ [+voiced uvular nasal -syllabic]
Symbol ʁ̞ [+voiced uvular approximant -syllabic]
Symbol ʟ̠ [+voiced uvular lateralapproximant -syllabic]

Symbol ħ [-voiced pharyngeal fricative -syllabic]
Symbol ʕ [+voiced pharyngeal fricative -syllabic]

Symbol ʡ [-voiced epiglottal plosive -syllabic]
Symbol ʜ [-voiced epiglottal fricative -syllabic]
Symbol ʡ͡ʜ [-voiced epiglottal affricate -syllabic]
Symbol ʢ [+voiced epiglottal fricative -syllabic]

Symbol ʔ [glottal plosive -syllabic]
Symbol h [-voiced glottal fricative -syllabic]
Symbol ʔ͡h [-voiced glottal affricate -syllabic]
Symbol ɦ [+voiced glottal fricative -syllabic]

Element consonant {[plosive], [implosive], [fricative], [affricate], [trill], [flap], [approximant], [lateralapproximant], [lateralfricative], [lateralaffricate], [nasal]}
Element vowel {[front], [nearfront], [central], [nearback], [back]}
Element obstruent {[affricate], [plosive], [implosive], [fricative], [lateralfricative], [lateralaffricate]}
Element sonorant {[!affricate !plosive !implosive !fricative !lateralfricative !lateralaffricate]&@consonant}
Element continuant {@sonorant, [fricative], [lateralfricative]}
Element laryngeal {[glottal], [epiglottal], [pharyngeal]}
Element labial {[labiodental], [labial]}
Element lax @vowel&{[nearfront], [nearhigh], [nearlow central]}
Element nonlax @vowel&!{[nearfront], [nearhigh], [nearlow central]}

deromanisation:
 {ng, y, kh} => {ŋ, ɣ, x}
 then: (
 {[plosive +voiced], [nasal], [lateralapproximant]} h => [+breathy] *
 {{â, â}, {ê, ê}, {î, î}, {ô, ô}, {û, û}, {ï̂, ï̂}, {ü̂, ü̂}, {ï, ï}, {ü, ü}} => {aː, eː, iː, oː, uː, ɯː, yː, ɯ, y}
 )
 then: (
 h => *
 * => h / @vowel _ @vowel
 )
 then: \' => *

assimilation defer:
 [nasal] => [$Place] / _ @obstruent&[$Place]

glomination:
 @vowel \- @vowel&[+long] => * * [-long]
 @vowel \- / _ @vowel => *
 \- => *

lengthening:
 []$1 $1 => * [+long]

syllables:
 @consonant? {@vowel&[+syllabic] {@vowel&[-syllabic], {@vowel&[-syllabic]}? @continuant? @consonant}, @vowel&[+syllabic +long]} => [+heavy]
 s? @consonant? @continuant? {@vowel&[+syllabic] {@vowel&[-syllabic], {@vowel&[-syllabic]}? @continuant? @consonant}, @vowel&[+syllabic +long]} => [+heavy] / {$, @consonant} _
 @consonant? @vowel&[+syllabic] => [+heavy] / _ @consonant&[+long]
 @consonant? @vowel&[+syllabic] => [-heavy]
 s? @consonant? @continuant? @vowel&[+syllabic] => [+heavy] / {$, @consonant} _ @consonant&[+long]
 s? @consonant? @continuant? @vowel&[+syllabic] => [-heavy] / {$, @consonant} _

syllables:
 explicit

dentalisation:
 [alveolar plosive] => [dental]

aspiration:
 [+breathy plosive] => [-breathy -voiced +aspirated]

fricative-lenition:
 [+voiced fricative] => [approximant]
 then: [velar approximant] => [palatal]
 then: [alveolar approximant] => [trill]
 then: [labiodental approximant] => [velar +labialised]

sonorant-shift:
 [+breathy labial nasal] => [-breathy velar approximant +labialised]
 [-breathy alveolar lateralapproximant] => [-breathy trill]
 [+breathy alveolar lateralapproximant] => [-breathy velar approximant]
 [+breathy alveolar nasal] => [-breathy alveolar lateralapproximant]
 [+breathy velar nasal] => [-breathy palatal approximant]

postalveolarisation:
 [alveolar fricative -voiced] => [postalveolar] / {@vowel, $} _ {@vowel, $}

palatalisation:
 {[velar], [alveolar plosive]} => [palatal] / _ {j, [front]}
 [velar] => [labial] / _ w
 then: [labial fricative] => [labiodental]

metathesis:
 @vowel$1 {[nasal], [trill]}$2 => $2 $1 / @vowel [approximant] _
 then: @vowel$1 {$1, a} => [+long] *
 then: @vowel&[highmid] => [high] / @vowel _

syllables:
 @consonant? {@vowel&[+syllabic] {@vowel&[-syllabic], {@vowel&[-syllabic]}? @continuant? @consonant}, @vowel&[+syllabic +long]} => [+heavy]
 s? @consonant? @continuant? {@vowel&[+syllabic] {@vowel&[-syllabic], {@vowel&[-syllabic]}? @continuant? @consonant}, @vowel&[+syllabic +long]} => [+heavy] / {$, @consonant} _
 @consonant? @vowel&[+syllabic] => [+heavy] / _ @consonant&[+long]
 @consonant? @vowel&[+syllabic] => [-heavy]
 s? @consonant? @continuant? @vowel&[+syllabic] => [+heavy] / {$, @consonant} _ @consonant&[+long]
 s? @consonant? @continuant? @vowel&[+syllabic] => [-heavy] / {$, @consonant} _

vowel-loss:
 @vowel&[-long] => * / @vowel [approximant] _ @sonorant&![approximant] @vowel
 @vowel&[+long] => [-long] / @vowel [approximant] _ @sonorant&![approximant] @vowel
 then: @vowel&[-long] => * / @vowel @consonant _ $
 then: @vowel&[+long] => [-long] / @vowel @consonant _ $

approximant-vowel-shift:
 [high back] => [highmid] / @vowel j _
 [highmid front] => [high] / @vowel j _
 [highmid back] => [front] / @vowel j _
 [low] => [highmid front -rounded] / @vowel j _

 [high front] => [back +rounded] / @vowel w _
 [highmid back] => [high +rounded] / @vowel w _
 [highmid front] => [back +rounded] / @vowel w _
 [low] => [highmid back +rounded] / @vowel w _

 [high front] => [back -rounded] / @vowel ɰ _
 [highmid back] => [high -rounded] / @vowel ɰ _
 [highmid front] => [back -rounded] / @vowel ɰ _
 [low] => [highmid back -rounded] / @vowel ɰ _

 [high back] => [highmid] / _ j @vowel
 [highmid front] => [high] / _ j @vowel
 [highmid back] => [front] / _ j @vowel
 [low] => [highmid front -rounded] / _ j @vowel

 [high front] => [back +rounded] / _ w @vowel
 [highmid back] => [high +rounded] / _ w @vowel
 [highmid front] => [back +rounded] / _ w @vowel
 [low] => [highmid back +rounded] / _ w @vowel

 [high front] => [back -rounded] / _ ɰ @vowel
 [highmid back] => [high -rounded] / _ ɰ @vowel
 [highmid front] => [back -rounded] / _ ɰ @vowel
 [low] => [highmid back -rounded] / _ ɰ @vowel

 j [high front] => * [+long] // @vowel _
 j [high back] => * [highmid] // @vowel _
 j [highmid front] => * [high] // @vowel _
 j [highmid back] => * [front] // @vowel _
 j [low] => * [highmid front -rounded] // @vowel _

 w [high back] => * [+long +rounded] // @vowel _
 w [high front] => * [back +rounded] // @vowel _
 w [highmid back] => * [high +rounded] // @vowel _
 w [highmid front] => * [back +rounded] // @vowel _
 w [low] => * [highmid back +rounded] // @vowel _

 ɰ [high back] => * [+long -rounded] // @vowel _
 ɰ [high front] => * [back -rounded] // @vowel _
 ɰ [highmid back] => * [high -rounded] // @vowel _
 ɰ [highmid front] => * [back -rounded] // @vowel _
 ɰ [low] => * [highmid back -rounded] // @vowel _

 [high front] j => [+long] * // _ @vowel
 [high back] j => [highmid] * // _ @vowel
 [highmid front] j => [high] * // _ @vowel
 [highmid back] j => [front] * // _ @vowel
 [low] j => [highmid front -rounded] * // _ @vowel

 [high back] w => [+long +rounded] * // _ @vowel
 [high front] w => [back +rounded] * // _ @vowel
 [highmid back] w => [high +rounded] * // _ @vowel
 [highmid front] w => [back +rounded] * // _ @vowel
 [low] w => [highmid back +rounded] * // _ @vowel

 [high back] ɰ => [+long -rounded] * // _ @vowel
 [high front] ɰ => [back -rounded] * // _ @vowel
 [highmid back] ɰ => [high -rounded] * // _ @vowel
 [highmid front] ɰ => [back -rounded] * // _ @vowel
 [low] ɰ => [highmid back -rounded] * // _ @vowel

romanizer-old-corothic:
 unchanged

vocalisation:
 {j, w, ɰ} => {i̯, u̯, ɯ̯} / @vowel _ // _ @vowel

diphthongisation:
 * [high front +long] => e [-long -syllabic]
 * [high back +long +rounded] => o [-long -syllabic]
 * [high back +long -rounded] => ɤ [-long -syllabic]
 [highmid +long] => [high -long]
 [low +long] => [front nearlow -rounded -long]
 [low] => [back +rounded]

vowel-harmony @vowel:
 [high +syllabic] => [highmid] / $ {[low], [highmid]} {[]+}? _
 [highmid] => [low] / $ [low] {[]+}? _
 [highmid] => [high] / $ [high] {[]+}? _
 [low] => [highmid] / $ {[high], [highmid]} {[]+}? _
 then: [low back -rounded] => [front nearlow]

vowel-heightening:
 [highmid] => [high] / _ $

frication:
 [+aspirated plosive] => [fricative -aspirated]

debuccalisation:
 [labial fricative] => [glottal]
 [velar fricative] => [labiodental] / {[+rounded] _, _ [+rounded]}
 then: h => * // _ @vowel

retroflexation:
 [postalveolar] => [retroflex]
 {[dental], [alveolar]} => [retroflex] / {_ r, r _}
 then: r => * / {[retroflex] _, _ [retroflex]}
 then: [alveolar trill] => [retroflex approximant]

syllables:
 @consonant? {@vowel&[+syllabic] {@vowel&[-syllabic], {@vowel&[-syllabic]}? @continuant? @consonant}, @vowel&[+syllabic +long]} => [+heavy]
 s? @consonant? @continuant? {@vowel&[+syllabic] {@vowel&[-syllabic], {@vowel&[-syllabic]}? @continuant? @consonant}, @vowel&[+syllabic +long]} => [+heavy] / {$, @consonant} _
 @consonant? @vowel&[+syllabic] => [+heavy] / _ @consonant&[+long]
 @consonant? @vowel&[+syllabic] => [-heavy]
 s? @consonant? @continuant? @vowel&[+syllabic] => [+heavy] / {$, @consonant} _ @consonant&[+long]
 s? @consonant? @continuant? @vowel&[+syllabic] => [-heavy] / {$, @consonant} _

stress:
 (
 <syl>&[+heavy] => [+stress] / $ _ <syl> $
 else propagate: <syl> => [+stress] / {$, <syl>&[+stress]} <syl>&[-stress] _
 )
 then: <syl>&[+stress] => [-stress] / _ $ // <syl>&[+stress] {[]+}? _

romanizer-middle-corothic:
 unchanged

stress-marking:
 @vowel&[+syllabic +stress] => [hightone]

syllables:
 clear

romanizer:
 @vowel&[+long] * => [-long] ̂
 @consonant&[+long]$1 => $1 $1
 then: [+long] => [-long]
 then: @vowel => [+syllabic]
 then: (
 [nasal $Place] => m / _ @consonant&[$Place]&{[labial], [labiodental]}
 [nasal $Place] => n / _ @consonant&[$Place]
 )
 then: [dental plosive] => [alveolar]
 then: (
 {ǽ, ɒ́, ý, ɯ́, ǿ, ɤ́} => {a̋, á, ű, i̋, ő, e̋}
 {æ, ɒ, y, ɯ, ø, ɤ} => {ä, a, ü, ï, ö, ë}
 )
 then: {θ, x, ɰ, ng, ŋ, ɻ, ts, s / {@vowel, $} _ {@vowel, $}, ʂ} => {th, kh, ÿ, n'g, ng, r, z, sz, s}
 then: {d͡ʐ, t͡ʂ, ɖ, ʈ} => {dz, ts, rd, rt}
 then: {c, ɟ, ç, ʝ, j, ʎ, ɲ} => {c, j, ch, jh, y, ly, ny}
 then: {h, z, g, y} => * / @consonant$1 _ $1