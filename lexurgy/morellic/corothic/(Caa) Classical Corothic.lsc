Feature +breathy, +rounded, +long, +syllabic
Feature Frontness(front, nearfront, central, nearback, back)
Feature Height(low, nearlow, lowmid, mid, highmid, nearhigh, high)
Feature Place(labial, labiodental, dental, alveolar, postalveolar, retroflex, alveopalatal, palatal, velar, uvular, pharyngeal, epiglottal, glottal)
Feature Manner(plosive, fricative, affricate, implosive, trill, flap, approximant, lateralapproximant, lateralfricative, lateralaffricate, nasal)
Feature +voiced, +labialised, +palatalised, +aspirated, +nasalised, +rhotic, +retracted, +prenasalised, +pharyngealised, +ejective, +breathyvoice, +noaudiblerelease
Feature Tone(*notone, lowtone, midtone, hightone)
Feature (syllable) +heavy
Feature (syllable) +stress

Diacritic ʱ [+breathy]
Diacritic ̤ [+breathyvoice]
Diacritic ̃ [+nasalised]
Diacritic ̥ [-voiced]
Diacritic ̬ [+voiced]
Diacritic ̹ [+rounded]
Diacritic ̜ [-rounded]
Diacritic ˈ (before) [+stress]
Diacritic ᴴ [+heavy]
Diacritic ̠ [+retracted]
Diacritic ̩ [+syllabic]
Diacritic ̯ [-syllabic]
Diacritic ́ (floating) [hightone]
Diacritic ̀ (floating) [lowtone]
Diacritic ̄ (floating) [midtone]
Diacritic ˞ [+rhotic]
Diacritic ʰ [+aspirated]
Diacritic ʷ [+labialised]
Diacritic ʲ [+palatalised]
Diacritic ː [+long]
Diacritic ⁿ (before) [+prenasalised]
Diacritic ˤ [+pharyngealised]
Diacritic ʼ [+ejective]
Diacritic ̚ [+noaudiblerelease]

Symbol i [front high -rounded +voiced +syllabic]
Symbol y [front high +rounded +voiced +syllabic]
Symbol ɪ [nearfront nearhigh -rounded +voiced +syllabic]
Symbol ʏ [nearfront nearhigh +rounded +voiced +syllabic]
Symbol e [front highmid -rounded +voiced +syllabic]
Symbol ø [front highmid +rounded +voiced +syllabic]
Symbol e̞ [front mid -rounded +voiced +syllabic]
Symbol ø̞ [front mid +rounded +voiced +syllabic]
Symbol ɛ [front lowmid -rounded +voiced +syllabic]
Symbol œ [front lowmid +rounded +voiced +syllabic]
Symbol a [front low -rounded +voiced +syllabic]
Symbol ɶ [front low +rounded +voiced +syllabic]
Symbol æ [front nearlow -rounded +voiced +syllabic]
Symbol ɨ [central high -rounded +voiced +syllabic]
Symbol ʉ [central high +rounded +voiced +syllabic]
Symbol ɘ [central highmid -rounded +voiced +syllabic]
Symbol ɵ [central highmid +rounded +voiced +syllabic]
Symbol ə [central mid -rounded +voiced +syllabic]
Symbol ɵ̞ [central mid +rounded +voiced +syllabic]
Symbol ɜ [central lowmid -rounded +voiced +syllabic]
Symbol ɞ [central lowmid +rounded +voiced +syllabic]
Symbol ä [central low -rounded +voiced +syllabic]
Symbol ɶ̈ [central low +rounded +voiced +syllabic]
Symbol ɐ [central nearlow -rounded +voiced +syllabic]
Symbol u [back high +rounded +voiced +syllabic]
Symbol ɯ [back high -rounded +voiced +syllabic]
Symbol ʊ [nearback nearhigh +rounded +voiced +syllabic]
Symbol ʊ̹ [nearback nearhigh -rounded +voiced +syllabic]
Symbol o [back highmid +rounded +voiced +syllabic]
Symbol ɤ [back highmid -rounded +voiced +syllabic]
Symbol o̞ [back mid +rounded +voiced +syllabic]
Symbol ɤ̞ [back mid -rounded +voiced +syllabic]
Symbol ɔ [back lowmid +rounded +voiced +syllabic]
Symbol ʌ [back lowmid -rounded +voiced +syllabic]
Symbol ɒ [back low +rounded +voiced +syllabic]
Symbol ɑ [back low -rounded +voiced +syllabic]

Symbol p [-voiced labial plosive -syllabic]
Symbol ɸ [-voiced labial fricative -syllabic]
Symbol p͡ɸ [-voiced labial affricate -syllabic]
Symbol b [+voiced labial plosive -syllabic]
Symbol ɓ [+voiced labial implosive -syllabic]
Symbol ᵐb [+voiced labial plosive -syllabic +prenasalised]
Symbol β [+voiced labial fricative -syllabic]
Symbol b͡β [+voiced labial affricate -syllabic]
Symbol ʙ [+voiced labial trill -syllabic]
Symbol ʙ̆ [+voiced labial flap -syllabic]
Symbol m [+voiced labial nasal -syllabic]
Symbol β̞ [+voiced labial approximant -syllabic]

Symbol p̪ [-voiced labiodental plosive -syllabic]
Symbol f [-voiced labiodental fricative -syllabic]
Symbol p̪͡f [-voiced labiodental affricate -syllabic]
Symbol b̪ [+voiced labiodental plosive -syllabic]
Symbol ɓ̪ [+voiced labiodental implosive -syllabic]
Symbol v [+voiced labiodental fricative -syllabic]
Symbol b̪͡v [+voiced labiodental affricate -syllabic]
Symbol ʙ̠ [+voiced labiodental trill -syllabic]
Symbol ⱱ [+voiced labiodental flap -syllabic]
Symbol ɱ [+voiced labiodental nasal -syllabic]
Symbol ʋ [+voiced labiodental approximant -syllabic]

Symbol t [-voiced alveolar plosive -syllabic]
Symbol s [-voiced alveolar fricative -syllabic]
Symbol t͡s [-voiced alveolar affricate -syllabic]
Symbol d [+voiced alveolar plosive -syllabic]
Symbol ɗ [+voiced alveolar implosive -syllabic]
Symbol z [+voiced alveolar fricative -syllabic]
Symbol d͡z [+voiced alveolar affricate -syllabic]
Symbol r [+voiced alveolar trill -syllabic]
Symbol ɾ [+voiced alveolar flap -syllabic]
Symbol n [+voiced alveolar nasal -syllabic]
Symbol ɹ [+voiced alveolar approximant -syllabic]
Symbol l [+voiced alveolar lateralapproximant -syllabic]
Symbol ɬ [-voiced alveolar lateralfricative -syllabic]
Symbol ɮ [+voiced alveolar lateralfricative -syllabic]
Symbol t͡ɬ [-voiced alveolar lateralaffricate -syllabic]
Symbol d͡ɮ [+voiced alveolar lateralaffricate -syllabic]

Symbol t̠ [-voiced postalveolar plosive -syllabic]
Symbol ʃ [-voiced postalveolar fricative -syllabic]
Symbol t͡ʃ [-voiced postalveolar affricate -syllabic]
Symbol d̠ [+voiced postalveolar plosive -syllabic]
Symbol ɗ̠ [+voiced postalveolar implosive -syllabic]
Symbol ʒ [+voiced postalveolar fricative -syllabic]
Symbol d͡ʒ [+voiced postalveolar affricate -syllabic]
Symbol r̠ [+voiced postalveolar trill -syllabic]
Symbol ɾ̠ [+voiced postalveolar flap -syllabic]
Symbol n̠ [+voiced postalveolar nasal -syllabic]
Symbol ɹ̠ [+voiced postalveolar approximant -syllabic]
Symbol l̠ [+voiced postalveolar lateralapproximant -syllabic]

Symbol t̪ [-voiced dental plosive -syllabic]
Symbol θ [-voiced dental fricative -syllabic]
Symbol t͡θ [-voiced dental affricate -syllabic]
Symbol d̪ [+voiced dental plosive -syllabic]
Symbol ɗ̪ [+voiced dental implosive -syllabic]
Symbol ð [+voiced dental fricative -syllabic]
Symbol d͡ð [+voiced dental affricate -syllabic]
Symbol r̪ [+voiced dental trill -syllabic]
Symbol ɾ̪ [+voiced dental flap -syllabic]
Symbol n̪ [+voiced dental nasal -syllabic]
Symbol ð̞ [+voiced dental approximant -syllabic]
Symbol θ̞ [-voiced dental approximant -syllabic]
Symbol l̪ [+voiced dental lateralapproximant -syllabic]

Symbol ʈ [-voiced retroflex plosive -syllabic]
Symbol ʂ [-voiced retroflex fricative -syllabic]
Symbol t͡ʂ [-voiced retroflex affricate -syllabic]
Symbol ɖ [+voiced retroflex plosive -syllabic]
Symbol ᶑ [+voiced retroflex implosive -syllabic]
Symbol ʐ [+voiced retroflex fricative -syllabic]
Symbol d͡ʐ [+voiced retroflex affricate -syllabic]
Symbol ɽː [+voiced retroflex trill -syllabic]
Symbol ɽ [+voiced retroflex flap -syllabic]
Symbol ɳ [+voiced retroflex nasal -syllabic]
Symbol ɻ [+voiced retroflex approximant -syllabic]
Symbol ɭ [+voiced retroflex lateralapproximant -syllabic]

Symbol c̟ [-voiced alveopalatal plosive -syllabic]
Symbol ɕ [-voiced alveopalatal fricative -syllabic]
Symbol t͡ɕ [-voiced alveopalatal affricate -syllabic]
Symbol ɟ̟ [+voiced alveopalatal plosive -syllabic]
Symbol ʄ̟ [+voiced alveopalatal implosive -syllabic]
Symbol ʑ [+voiced alveopalatal fricative -syllabic]
Symbol d͡ʑ [+voiced alveopalatal affricate -syllabic]
Symbol ɟ̟̆ː [+voiced alveopalatal trill -syllabic]
Symbol ɟ̟̆ [+voiced alveopalatal flap -syllabic]
Symbol ɲ̟ [+voiced alveopalatal nasal -syllabic]
Symbol j̟ [+voiced alveopalatal approximant -syllabic]
Symbol ʎ̟ [+voiced alveopalatal lateralapproximant -syllabic]

Symbol c [-voiced palatal plosive -syllabic]
Symbol ç [-voiced palatal fricative -syllabic]
Symbol c͡ç [-voiced palatal affricate -syllabic]
Symbol ɟ [+voiced palatal plosive -syllabic]
Symbol ʄ [+voiced palatal implosive -syllabic]
Symbol ᶮɟ [+voiced +prenasalised palatal plosive -syllabic]
Symbol ʝ [+voiced palatal fricative -syllabic]
Symbol ɟ͡ʝ [+voiced palatal affricate -syllabic]
Symbol ɟ̆ː [+voiced palatal trill -syllabic]
Symbol ɟ̆ [+voiced palatal flap -syllabic]
Symbol ɲ [+voiced palatal nasal -syllabic]
Symbol j [+voiced palatal approximant -syllabic]
Symbol ɥ [+voiced palatal approximant +labialised -syllabic]
Symbol ʎ [+voiced palatal lateralapproximant -syllabic]

Symbol k [-voiced velar plosive -syllabic]
Symbol x [-voiced velar fricative -syllabic]
Symbol k͡x [-voiced velar affricate -syllabic]
Symbol g [+voiced velar plosive -syllabic]
Symbol ɠ [+voiced velar implosive -syllabic]
Symbol ᵑg [+voiced velar plosive -syllabic +prenasalised]
Symbol ɣ [+voiced velar fricative -syllabic]
Symbol g͡ɣ [+voiced velar affricate -syllabic]
Symbol ŋ [+voiced velar nasal -syllabic]
Symbol ŋ̊ [-voiced velar nasal -syllabic]
Symbol ɰ [+voiced velar approximant -syllabic]
Symbol w [+voiced velar approximant +labialised -syllabic]
Symbol ʍ [-voiced velar approximant +labialised -syllabic]
Symbol ʟ [+voiced velar lateralapproximant -syllabic]

Symbol q [-voiced uvular plosive -syllabic]
Symbol χ [-voiced uvular fricative -syllabic]
Symbol q͡χ [-voiced uvular affricate -syllabic]
Symbol ɢ [+voiced uvular plosive -syllabic]
Symbol ʛ [+voiced uvular implosive -syllabic]
Symbol ʁ [+voiced uvular fricative -syllabic]
Symbol ɢ͡ʁ [+voiced uvular affricate -syllabic]
Symbol ʀ [+voiced uvular trill -syllabic]
Symbol ɢ̆ [+voiced uvular flap -syllabic]
Symbol ɴ [+voiced uvular nasal -syllabic]
Symbol ʁ̞ [+voiced uvular approximant -syllabic]
Symbol ʟ̠ [+voiced uvular lateralapproximant -syllabic]

Symbol ħ [-voiced pharyngeal fricative -syllabic]
Symbol ʕ [+voiced pharyngeal fricative -syllabic]

Symbol ʡ [-voiced epiglottal plosive -syllabic]
Symbol ʜ [-voiced epiglottal fricative -syllabic]
Symbol ʡ͡ʜ [-voiced epiglottal affricate -syllabic]
Symbol ʢ [+voiced epiglottal fricative -syllabic]

Symbol ʔ [glottal plosive -syllabic]
Symbol h [-voiced glottal fricative -syllabic]
Symbol ʔ͡h [-voiced glottal affricate -syllabic]
Symbol ɦ [+voiced glottal fricative -syllabic]

Element consonant {[plosive], [implosive], [fricative], [affricate], [trill], [flap], [approximant], [lateralapproximant], [lateralfricative], [lateralaffricate], [nasal]}
Element vowel {[front], [nearfront], [central], [nearback], [back]}
Element obstruent {[affricate], [plosive], [implosive], [fricative], [lateralfricative], [lateralaffricate]}
Element sonorant {[!affricate !plosive !implosive !fricative !lateralfricative !lateralaffricate]&@consonant}
Element continuant {@sonorant, [fricative], [lateralfricative]}
Element laryngeal {[glottal], [epiglottal], [pharyngeal]}
Element labial {[labiodental], [labial]}
Element lax @vowel&{[nearfront], [nearhigh], [nearlow central]}
Element nonlax @vowel&!{[nearfront], [nearhigh], [nearlow central]}

deromanisation:
 {ng, y, kh} => {ŋ, ɣ, x}
 then: (
 {[plosive +voiced], [nasal], [lateralapproximant]} h => [+breathy] *
 {{â, â}, {ê, ê}, {î, î}, {ô, ô}, {û, û}, {ï̂, ï̂}, {ü̂, ü̂}, {ï, ï}, {ü, ü}} => {aː, eː, iː, oː, uː, ɯː, yː, ɯ, y}
 )
 then: (
 h => *
 * => h / @vowel _ @vowel
 )
 then: \' => *

assimilation defer:
 [nasal] => [$Place] / _ @obstruent&[$Place]
 then: [dental nasal] => [alveolar]

lengthening:
 []$1 $1 => * [+long]

glomination:
 @vowel&[+long] \- @vowel => [-long] * *
 @vowel \- / _ @vowel => *
 \- => *

syllables:
 @consonant? {@vowel&[+syllabic] {@vowel&[-syllabic], {@vowel&[-syllabic]}? @continuant? @consonant}, @vowel&[+syllabic +long]} => [+heavy]
 s? @consonant? @continuant? {@vowel&[+syllabic] {@vowel&[-syllabic], {@vowel&[-syllabic]}? @continuant? @consonant}, @vowel&[+syllabic +long]} => [+heavy] / {$, @consonant} _
 @consonant? @vowel&[+syllabic] => [+heavy] / _ @consonant&[+long]
 @consonant? @vowel&[+syllabic] => [-heavy]
 s? @consonant? @continuant? @vowel&[+syllabic] => [+heavy] / {$, @consonant} _ @consonant&[+long]
 s? @consonant? @continuant? @vowel&[+syllabic] => [-heavy] / {$, @consonant} _

syllables:
 explicit

dentalisation:
 [alveolar plosive] => [dental]

vowel-smoothing:
 @vowel [+voiced velar fricative] @vowel => [+long] * *

breathy-shift:
 [+breathy plosive] => [-voiced -breathy +aspirated]
 [+breathy labial nasal] => [+labialised velar approximant]
 [+breathy alveolar lateralapproximant] => [+labialised velar approximant] // _ @vowel
 [+breathy alveolar lateralapproximant] => [-voiced lateralfricative]
 [+breathy alveolar nasal] => [lateralapproximant]
 [+breathy velar nasal] => [palatal approximant]

continuant-shift-and-lengthening:
 [-voiced fricative]&![alveolar] => [glottal]
 [+voiced labiodental fricative] => [+labialised velar approximant]
 [+voiced alveolar fricative] => [-voiced affricate] // . @consonant _ @vowel
 [+voiced alveolar fricative] => [-voiced]
 [+voiced velar fricative] => [palatal approximant]
 then: @vowel$1 $1 => [+long] *
 then: @vowel [glottal] => [+long] * // _ @vowel
 then: [glottal] => *

palatalisation defer:
 {a, e, i, y, o, u, ɯ} => {e, i, i, y, e, o, i} / j _
 {a, e, i, y, o, u, ɯ} => {o, o, e, u, u, u, ɯ} / w _
 then: (
 [velar !approximant] j => [palatal] *
 [alveolar]&@sonorant j => [palatal] *
 )
 then: j => * / @consonant _ @vowel

palatalisation-1:
 :palatalisation

lateral-shift:
 [alveolar plosive] [lateralapproximant] => [-voiced lateralaffricate] *
 [-breathy alveolar lateralapproximant] => [trill]
 then: [+breathy] => [-breathy]

romanizer-old-corothic:
 unchanged

vowel-loss:
 @vowel&[-long] => * / @vowel&[+syllabic] @continuant? @consonant _ $
 then: {j, w} [low]? => {i, u} * / @vowel _ // _ @vowel
 then: @vowel$1 {j, w} $1 => [] {i, u} * // _ @vowel
 then: {i, u} => {j, w} / _ @vowel // _ @vowel*2
 then: @vowel => [-syllabic] / @vowel _ // @vowel*2 _

delengthening:
 @vowel => [-long] / {$ _, _ {@vowel, @sonorant&![nasal]}, @vowel _, _ {@consonant*}? $} // $ @consonant? _ @consonant? $
 then: [+long] => [-long] / _ $
 then: @consonant&[+long] => [-long] / @vowel&[+long] _
 then: :palatalisation

syllables:
 @consonant? {@vowel&[+syllabic] {@vowel&[-syllabic], {@vowel&[-syllabic]}? @continuant? @consonant}, @vowel&[+syllabic +long]} => [+heavy]
 s? @consonant? @continuant? {@vowel&[+syllabic] {@vowel&[-syllabic], {@vowel&[-syllabic]}? @continuant? @consonant}, @vowel&[+syllabic +long]} => [+heavy] / {$, @consonant} _
 @consonant? @vowel&[+syllabic] => [+heavy] / _ @consonant&[+long]
 @consonant? @vowel&[+syllabic] => [-heavy]
 s? @consonant? @continuant? @vowel&[+syllabic] => [+heavy] / {$, @consonant} _ @consonant&[+long]
 s? @consonant? @continuant? @vowel&[+syllabic] => [-heavy] / {$, @consonant} _

postalveolarisation:
 [alveolar fricative] => [postalveolar] // {@consonant _, _ @sonorant}
 then: (
 [alveolar fricative] j => [postalveolar] *
 [affricate] => [fricative]
 @obstruent => * / _ s
 )
 then: :assimilation

syllables:
 @consonant? {@vowel&[+syllabic] {@vowel&[-syllabic], {@vowel&[-syllabic]}? @continuant? @consonant}, @vowel&[+syllabic +long]} => [+heavy]
 {s, ʃ}? @consonant? @continuant? {@vowel&[+syllabic] {@vowel&[-syllabic], {@vowel&[-syllabic]}? @continuant? @consonant}, @vowel&[+syllabic +long]} => [+heavy] / {$, @consonant} _
 @consonant? @vowel&[+syllabic] => [+heavy] / _ @consonant&[+long]
 @consonant? @vowel&[+syllabic] => [-heavy]
 {s, ʃ}? @consonant? @continuant? @vowel&[+syllabic] => [+heavy] / {$, @consonant} _ @consonant&[+long]
 {s, ʃ}? @consonant? @continuant? @vowel&[+syllabic] => [-heavy] / {$, @consonant} _

stress propagate:
 <syl> => [+stress] / _ <syl> {$, <syl>&[+stress]}
 then: <syl> => [-stress] / _ $ // $ <syl> _

romanizer-middle-corothic:
 unchanged

labialisation:
 * => w / [velar]&@obstruent w {{[]&!{[velar]&@obstruent}}*}&!{@vowel*} [velar]&@obstruent _
 then: [velar]&@obstruent w => [labial] *

devoicing:
 [nasal] [-voiced]&@obstruent => [-voiced] *

uvularisation:
 [-voiced velar]&@obstruent => [uvular] / {@vowel _, _ @vowel} // {$, @consonant} ʃ _
 [+voiced velar]&@obstruent => [-voiced]

vowel-harmony @vowel:
 [low] => [highmid] / $ {[high], [highmid]}&[+syllabic] []* _
 [high]&[+syllabic] => [highmid] / $ {[low], [highmid]}&[+syllabic] []* _
 [highmid] => [$Height] / $ [$Height]&[+syllabic] []* _
 then: @vowel&[!low] => [$rounded] / _ [$rounded -syllabic]

lengthening-2:
 @vowel&[$Frontness $Height] @vowel&[$Frontness $Height] => [+long] *
 then: @consonant&[+long] => [-long] / @vowel&[+long] _

frication:
 [+aspirated] => [-aspirated fricative]
 then: [labial fricative] => [glottal]
 then: [palatal approximant] => [-voiced postalveolar fricative] / _ @sonorant

romanizer-classical-corothic:
 unchanged

syllable-marking:
 @vowel&[+syllabic +stress] => [hightone]

syllables:
 clear

romanizer:
 [+long]$1 => $1 $1
 then: [+long] => [-long]
 then: @vowel => [+syllabic]
 then: (
 [nasal $Place] => m / _ @consonant&[$Place]&{[labial], [labiodental]}
 [nasal $Place] => n / _ @consonant&[$Place]
 )
 then: [nasal -voiced] * => [+voiced] h
 then: {θθ, θ, χχ, χ} => {tth, th, qqh, qh}
 then: [dental] => [alveolar]
 then: {ɯ́, ý, ɤ́, ǿ, ɒ́, ɯ, y, ɤ, ø, ɒ} => {i̋, ű, e̋, ő, ȁ, ï, ü, ë, ö, à}
 then: {ʍʍ, ʍ, ʃ, ss, s, ɬɬ, ɬ, ng, ŋ} => {wwh, wh, s, ssz, sz, llh, lh, n'g, ng}
 then: {ɟ, ɲɲ, ɲ, ʎʎ, ʎ} => {g, nnj, nj, llj, lj}